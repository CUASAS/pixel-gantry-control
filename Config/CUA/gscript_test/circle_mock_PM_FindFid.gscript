LOADCONFIG
SETLOG Phase1SuctionCupsv2_GM0117.txt
SETERRORMODE prompt
CLEARLOG
HOME
COPY $speed 100
COPY $fid_speed 100
COPY $fid_wait 500
COPY $rspeed 10
COPY $img_idx 0
COPY $counter 0

# PM RIGHT
COPY $start_tr {343.274138,528.844888,68.052883}
COPY $start_br {343.236452,572.045900,68.074782}
COPY $start_bl {326.035632,572.026669,68.107886}
COPY $start_tl {326.077863,528.827758,68.080988}

FIT $start_pos $start_rot TFPX_1X2_MOCKUP $start_tr $start_br $start_bl $start_tl

PRINT "STARTPOS|ROT %v %v" $start_pos $start_rot

# PM LEFT
COPY $end_tr {268.246199,530.011035,68.122553}
COPY $end_br {267.807541,573.207475,68.125128}
COPY $end_bl {250.602455,573.031970,68.097586}
COPY $end_tl {251.038638,529.832901,68.065100}

FIT $end_pos $end_rot TFPX_1X2_MOCKUP $end_tr $end_br $end_bl $end_tl

PRINT "ENDPOS|ROT %v %v" $end_pos $end_rot

FLEXREAD $tl_local geometry.TFPX_1X2_MOCKUP.fid_tl
FLEXREAD $tr_local geometry.TFPX_1X2_MOCKUP.fid_tr
FLEXREAD $br_local geometry.TFPX_1X2_MOCKUP.fid_br
FLEXREAD $bl_local geometry.TFPX_1X2_MOCKUP.fid_bl

@PROCESS_START SETVAC purdue_mechanics_right 1
INC $counter $counter
COPY $pos $start_pos
COPY $rot $start_rot
CALL @SURVEY

COPY $start_pos $meas_pos
COPY $start_rot $meas_rot

PRINT "Updated start_pos|start_rot%v %v" $start_pos $start_rot

LOADTOOL picker_tool

#Pickup Step
COPY $target_pos $start_pos
CALL @CALCPICKUP
PRINT "Centroid Z0 : %v" $pickup_pos_z0
MOVETO $pickup_pos_z0 $speed
ROTATE $start_rot $rspeed
COPY $centroid_start_z0 $pickup_pos_z0
COPY $centroid_start $pickup_pos
MOVETO $pickup_pos 10
SETVAC gantry_head_inner 1
WAIT $fid_wait 
SETVAC purdue_mechanics_right 0
WAIT $fid_wait
MOVETO $pickup_pos_z0 10

INVERT $start_rot_ $start_rot
ROTATE $start_rot_ $rspeed

# Place Step
COPY $target_pos $end_pos
CALL @CALCPICKUP
MOVETO $pickup_pos_z0 $speed
COPY $centroid_end_z0 $pickup_pos_z0
COPY $centroid_end $pickup_pos
ROTATE $end_rot $rspeed
MOVETO $pickup_pos 10
WAIT $fid_wait
SETVAC purdue_mechanics_left 1
SETVAC gantry_head_inner 0
WAIT $fid_wait
MOVETO $pickup_pos_z0 10
INVERT $end_rot_ $end_rot
ROTATE $end_rot_ $rspeed

UNLOADTOOL

COPY $pos $end_pos
COPY $rot $end_rot
CALL @SURVEY

PRINT "MEAS_POS for END_POS %v" $meas_pos

SUB $delta $meas_pos $end_pos
COPY $meas_end_pos $meas_pos
SUB $delta_tl $end_tl $tl_meas
SUB $delta_tr $end_tr $tr_meas
SUB $delta_bl $end_bl $bl_meas 
SUB $delta_br $end_br $br_meas
PRINT "%d Misplacement: %v" $counter $delta
PRINT "    delta_tl:    %v " $delta_tl
PRINT "    delta_tr:    %v " $delta_tr
PRINT "    delta_bl:    %v " $delta_bl
PRINT "    delta_br:    %v " $delta_br
INVERT $neg_meas_rot $meas_rot
COMPOSE $delta_theta $end_rot $neg_meas_rot
PRINT "Misplacement Angle: %r" $delta_theta

MOVEREL {0,0,-60} $speed

LOADTOOL picker_tool
MOVETO $centroid_end_z0 $speed
MOVETO $centroid_end 10
SETVAC gantry_head_inner 1
WAIT $fid_wait
SETVAC purdue_mechanics_left 0
WAIT $fid_wait
MOVETO $centroid_end_z0 10
MOVETO $centroid_start_z0 $speed
MOVETO $centroid_start 10
WAIT $fid_wait
SETVAC purdue_mechanics_right 1
WAIT $fid_wait
SETVAC gantry_head_inner 0
WAIT $fid_wait
MOVETO $centroid_start_z0 $speed
UNLOADTOOL

#CHOICEPOPUP $choice "Move piece back to launchpad or quit" "I moved it" "Quit" 
#COPY $start_pos $meas_pos #Update start_pos so not to move the glass heater
#GOTOIF @PROCESS_START $choice
GOTO @PROCESS_START

END


# SURVEY
# Arguments:
#  $pos - expected position of piece
#  $rot - expected orientation of piece
#  $**_local - local coordinates of the fiducials
@SURVEY COPY $SURVEY_RET $RET
        MPGON
        MOVETO $pos $speed
        TRANSFORML2G $tl_global $tl_local $pos $rot
        MOVETO $tl_global $fid_speed
        WAIT $fid_wait
        FINDFID $tl_meas FPIX_MOCK
        PRINT "      tl_meas %v" $tl_meas
        CALL @SAVE_SNAPSHOT 


        TRANSFORML2G $tr_global $tr_local $pos $rot
        MOVETO $tr_global $fid_speed
        WAIT $fid_wait
        FINDFID $tr_meas FPIX_MOCK
        PRINT "      tr_meas %v" $tr_meas
        CALL @SAVE_SNAPSHOT


        TRANSFORML2G $br_global $br_local $pos $rot
        MOVETO $br_global $fid_speed
        WAIT $fid_wait
        FINDFID $br_meas FPIX_MOCK
        PRINT "      br_meas %v" $br_meas
        CALL @SAVE_SNAPSHOT


        TRANSFORML2G $bl_global $bl_local $pos $rot
        MOVETO $bl_global $fid_speed
        WAIT $fid_wait
        FINDFID $bl_meas FPIX_MOCK
        PRINT "      bl_meas %v" $bl_meas
        CALL @SAVE_SNAPSHOT


        MPGOFF
        FIT $meas_pos $meas_rot TFPX_1X2_MOCKUP $tr_meas $br_meas $bl_meas $tl_meas
        PRINT "Measured position: %v" $meas_pos
        PRINT "Measured rotation: %q (%r)" $meas_rot $meas_rot
        GOTO $SURVEY_RET


# SAVE_SNAPSHOT
# Arguments:
#  $img_idx - numerical suffix of image filename, get's incremented each time
@SAVE_SNAPSHOT SNAPSHOT gantryhead "fiducial_on_glass_{$img_idx}.png"
               INC $img_idx $img_idx
               GOTO $RET


# CALCPICKUP
# Arguments:
#  $target_pos - center of piece to pickup/place
@CALCPICKUP FLEXREAD $picker_height geometry.PICKER_TOOL.center_offset
            FLEXREAD $ghco          geometry.tool_holder_offset
            ADD $netoffset $picker_height $ghco
            SUB $pickup_pos $target_pos $netoffset
            COPY $pickup_pos_z0 $pickup_pos
            COPY $pickup_pos_z0.z 0
            GOTO $RET