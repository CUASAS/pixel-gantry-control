#FINDFID $pos FPIX_MOCK
#PRINT "FIDPOS %v" $pos
#GETPOS $position
#PRINT "GetPos %v" $position

LOADCONFIG
SETLOG pick_and_place_Phase1.txt
SETERRORMODE prompt
CLEARLOG
HOME
COPY $speed 50
COPY $fid_speed 50
COPY $fid_wait 1000
COPY $rspeed 10
COPY $img_idx 0


# PO37 ALIGNMENT TOP RIGHT
COPY $start_tr {702.856606,205.544923,62.561755}
COPY $start_br {703.021653,248.741570,62.605701}
COPY $start_bl {685.816485,248.804505,62.575678}
COPY $start_tl {685.650358,205.608935,62.556731}

FIT $start_pos $start_rot TFPX_1X2_MOCKUP $start_tr $start_br $start_bl $start_tl

PRINT "STARTPOS|ROT %v %v" $start_pos $start_rot

COPY $end_tr {702.900756,205.556900,62.603010}
COPY $end_br {703.024434,248.754527,62.639454}
COPY $end_bl {685.819257,248.804725,62.586922}
COPY $end_tl {685.690320,205.606042,62.605479}

FIT $end_pos $end_rot TFPX_1X2_MOCKUP $end_tr $end_br $end_bl $end_tl

PRINT "ENDPOS|ROT %v %v" $end_pos $end_rot

FLEXREAD $tl_local geometry.TFPX_1X2_MOCKUP.fid_tl
FLEXREAD $tr_local geometry.TFPX_1X2_MOCKUP.fid_tr
FLEXREAD $br_local geometry.TFPX_1X2_MOCKUP.fid_br
FLEXREAD $bl_local geometry.TFPX_1X2_MOCKUP.fid_bl

@PROCESS_START SETVAC module_chuck4_slot3 1
COPY $pos $start_pos
COPY $rot $start_rot
CALL @SURVEY

COPY $start_pos $meas_pos
COPY $start_rot $meas_rot

PRINT "Updated start_pos|start_rot%v %v" $start_pos $start_rot

LOADTOOL picker_tool

#Pickup Step
COPY $target_pos $meas_pos
CALL @CALCPICKUP
MOVETO $pickup_pos_z0 25
ROTATE $start_rot $rspeed
MOVETO $pickup_pos 10
SETVAC gantry_head_inner 1
WAIT $fid_wait 
SETVAC module_chuck4_slot3 0
WAIT $fid_wait
MOVETO $pickup_pos_z0 10
WAIT 5000
INVERT $start_rot_ $start_rot
ROTATE $start_rot_ $rspeed

# Place Step
COPY $target_pos $end_pos
CALL @CALCPICKUP
MOVETO $pickup_pos_z0 25
ROTATE $end_rot $rspeed
MOVETO $pickup_pos 10
WAIT $fid_wait
SETVAC module_chuck4_slot3 1
#SETVAC purdue_mechanics_left 1
SETVAC gantry_head_inner 0
WAIT $fid_wait
MOVETO $pickup_pos_z0 10
WAIT 5000
INVERT $end_rot_ $end_rot
ROTATE $end_rot_ $rspeed

UNLOADTOOL

COPY $pos $end_pos
COPY $rot $end_rot
CALL @SURVEY

PRINT "MEAS_POS for END_POS %v" $meas_pos

SUB $delta $meas_pos $end_pos
SUB $delta_tl $end_tl $tl_meas
SUB $delta_tr $end_tr $tr_meas
SUB $delta_bl $end_bl $bl_meas 
SUB $delta_br $end_br $br_meas
PRINT "Misplacement: %v" $delta
PRINT "    delta_tl:    %v " $delta_tl
PRINT "    delta_tr:    %v " $delta_tr
PRINT "    delta_bl:    %v " $delta_bl
PRINT "    delta_br:    %v " $delta_br
MOVEREL {0,0,-60} $speed
SETVAC module_chuck4_slot3 0
#SETVAC purdue_mechanics_left 0

CHOICEPOPUP $choice "Move piece back to launchpad or quit" "I moved it" "Quit" 
GOTOIF @PROCESS_START $choice

END


# SURVEY
# Arguments:
#  $pos - expected position of piece
#  $rot - expected orientation of piece
#  $**_local - local coordinates of the fiducials
@SURVEY COPY $SURVEY_RET $RET
        MPGON
        MOVETO $pos $speed
        TRANSFORML2G $tl_global $tl_local $pos $rot
        MOVETO $tl_global $fid_speed
        VIDEO
        WAIT $fid_wait
        #FINDFID $tl_meas FPIX_MOCK
        GETPOS $tl_meas
        MOVETO $tl_meas $fid_speed
        VIDEO
        PRINT "      tl_meas %v" $tl_meas
        CALL @SAVE_SNAPSHOT 


        TRANSFORML2G $tr_global $tr_local $pos $rot
        MOVETO $tr_global $fid_speed
        VIDEO
        WAIT $fid_wait
        #FINDFID $tr_meas FPIX_MOCK
        GETPOS $tr_meas
        MOVETO $tr_meas $fid_speed
        VIDEO
        PRINT "      tr_meas %v" $tr_meas
        CALL @SAVE_SNAPSHOT


        TRANSFORML2G $br_global $br_local $pos $rot
        MOVETO $br_global $fid_speed
        VIDEO
        WAIT $fid_wait
        #FINDFID $br_meas FPIX_MOCK
        GETPOS $br_meas
        MOVETO $br_meas $fid_speed
        VIDEO
        PRINT "      br_meas %v" $br_meas
        CALL @SAVE_SNAPSHOT


        TRANSFORML2G $bl_global $bl_local $pos $rot
        MOVETO $bl_global $fid_speed
        VIDEO
        WAIT $fid_wait
        #FINDFID $bl_meas FPIX_MOCK
        GETPOS $bl_meas
        MOVETO $bl_meas $fid_speed
        VIDEO
        PRINT "      bl_meas %v" $bl_meas
        CALL @SAVE_SNAPSHOT


        MPGOFF
        FIT $meas_pos $meas_rot TFPX_1X2_MOCKUP $tr_meas $br_meas $bl_meas $tl_meas
        PRINT "Measured position: %v" $meas_pos
        PRINT "Measured rotation: %q (%r)" $meas_rot $meas_rot
        GOTO $SURVEY_RET


# SAVE_SNAPSHOT
# Arguments:
#  $img_idx - numerical suffix of image filename, get's incremented each time
@SAVE_SNAPSHOT SNAPSHOT gantryhead "fiducial_on_glass_{$img_idx}.png"
               INC $img_idx $img_idx
               GOTO $RET


# CALCPICKUP
# Arguments:
#  $target_pos - center of piece to pickup/place
@CALCPICKUP FLEXREAD $picker_height geometry.PICKER_TOOL.center_offset
            FLEXREAD $ghco          geometry.tool_holder_offset
            ADD $netoffset $picker_height $ghco
            SUB $pickup_pos $target_pos $netoffset
            COPY $pickup_pos_z0 $pickup_pos
            COPY $pickup_pos_z0.z 0
            GOTO $RET