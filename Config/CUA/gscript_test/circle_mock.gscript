#FINDFID $pos FPIX_MOCK
#PRINT "FIDPOS %v" $pos
#GETPOS $position
#PRINT "GetPos %v" $position
LOADCONFIG
SETLOG pick_and_place.txt
SETERRORMODE prompt
CLEARLOG
HOME
COPY $speed 150
COPY $rspeed 45
COPY $img_idx 0
COPY $wtime 2000

COPY $start_tr {344.063171,528.790161,67.888330}
COPY $start_br {344.162023,571.994478,67.906930}
COPY $start_bl {326.959818,572.023248,67.964528}
COPY $start_tl {326.858746,528.826964,67.888330}

FIT $start_pos $start_rot TFPX_1X2_MOCKUP $start_tr $start_br $start_bl $start_tl

PRINT "STARTPOS|ROT %v %v" $start_pos $start_rot

COPY $end_tr {268.110431,530.273903,67.895462}
COPY $end_tl {250.904966,530.220720,67.895462}
COPY $end_bl {250.772104,573.414248,67.895462}
COPY $end_br {267.976315,573.464015,67.895462}

FIT $end_pos $end_rot TFPX_1X2_MOCKUP $end_tr $end_br $end_bl $end_tl

PRINT "ENDPOS|ROT %v %v" $end_pos $end_rot

FLEXREAD $tl_local geometry.TFPX_1X2_MOCKUP.fid_tl
FLEXREAD $tr_local geometry.TFPX_1X2_MOCKUP.fid_tr
FLEXREAD $br_local geometry.TFPX_1X2_MOCKUP.fid_br
FLEXREAD $bl_local geometry.TFPX_1X2_MOCKUP.fid_bl

@PROCESS_START SETVAC purdue_mechanics_right 1
COPY $pos $start_pos
COPY $rot $start_rot
CALL @SURVEY

LOADTOOL picker_tool


#Pickup Step
COPY $target_pos $meas_pos
CALL @CALCPICKUP
MOVETO $pickup_pos_z0 50
ROTATE $meas_rot $rspeed
MOVETO $pickup_pos 10
SETVAC gantry_head_inner 1
WAIT 500
SETVAC purdue_mechanics_right 0
WAIT 2000
MOVETO $pickup_pos_z0 10
PRINT "meas_rot = %v" $meas_rot
MUL $meas_rot_ $meas_rot -1
print "meas_rot_ = %v" $meas_rot_
ROTATE $meas_rot_ $rspeed

# Place Step
COPY $target_pos $end_pos
CALL @CALCPICKUP
MOVETO $pickup_pos_z0 50
ROTATE $end_rot $rspeed
MOVETO $pickup_pos 10
WAIT 1000
SETVAC purdue_mechanics_left 1
SETVAC gantry_head_inner 0
WAIT 2000
MOVETO $pickup_pos_z0 10
MUL $end_rot_ $end_rot -1
ROTATE $end_rot_ $rspeed

UNLOADTOOL

COPY $pos $end_pos
COPY $rot $end_rot
CALL @SURVEY

SUB $delta $meas_pos $end_pos
PRINT "Misplacement: %v" $delta

MOVEREL {0,0,-60} $speed
SETVAC purdue_mechanics_left 0

CHOICEPOPUP $choice "Move piece back to launchpad or quit" "I moved it" "Quit" 
GOTOIF @PROCESS_START $choice

END


# SURVEY
# Arguments:
#  $pos - expected position of piece
#  $rot - expected orientation of piece
#  $**_local - local coordinates of the fiducials
@SURVEY COPY $SURVEY_RET $RET
        MPGON
        MOVETO $pos $speed
        TRANSFORML2G $tl_global $tl_local $pos $rot
        MOVETO $tl_global $speed
        VIDEO
        WAIT $wtime
        FINDFID $tl_meas FPIX_MOCK
        MOVETO $tl_meas $speed
        VIDEO
        PRINT "tl_meas %v" $tl_meas
        CALL @SAVE_SNAPSHOT 


        TRANSFORML2G $tr_global $tr_local $pos $rot
        MOVETO $tr_global $speed
        VIDEO
        WAIT $wtime
        FINDFID $tr_meas FPIX_MOCK
        MOVETO $tr_meas $speed
        VIDEO
        PRINT "tr_meas %v" $tr_meas
        CALL @SAVE_SNAPSHOT


        TRANSFORML2G $br_global $br_local $pos $rot
        MOVETO $br_global $speed
        VIDEO
        WAIT $wtime
        FINDFID $br_meas FPIX_MOCK
        MOVETO $br_meas $speed
        VIDEO
        PRINT "br_meas %v" $br_meas
        CALL @SAVE_SNAPSHOT


        TRANSFORML2G $bl_global $bl_local $pos $rot
        MOVETO $bl_global $speed
        VIDEO
        WAIT $wtime
        FINDFID $bl_meas FPIX_MOCK
        MOVETO $bl_meas $speed
        VIDEO
        PRINT "bl_meas %v" $bl_meas
        CALL @SAVE_SNAPSHOT


        MPGOFF
        FIT $meas_pos $meas_rot TFPX_1X2_MOCKUP $tr_meas $br_meas $bl_meas $tl_meas
        PRINT "Measured position: %v" $meas_pos
        PRINT "Measured rotation: %q (%r)" $meas_rot $meas_rot
        GOTO $SURVEY_RET


# SAVE_SNAPSHOT
# Arguments:
#  $img_idx - numerical suffix of image filename, get's incremented each time
@SAVE_SNAPSHOT SNAPSHOT gantryhead "fiducial_on_glass_{$img_idx}.png"
               INC $img_idx $img_idx
               GOTO $RET


# CALCPICKUP
# Arguments:
#  $target_pos - center of piece to pickup/place
@CALCPICKUP FLEXREAD $picker_height geometry.PICKER_TOOL.center_offset
            FLEXREAD $ghco          geometry.tool_holder_offset
            ADD $netoffset $picker_height $ghco
            SUB $pickup_pos $target_pos $netoffset
            COPY $pickup_pos_z0 $pickup_pos
            COPY $pickup_pos_z0.z 0
            GOTO $RET