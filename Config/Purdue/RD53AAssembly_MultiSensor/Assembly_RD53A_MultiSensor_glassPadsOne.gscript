VERSION 1.3

######################## RD53A Multiple Sensor Assembly - No Survey #############################
# Steps:
# 1. Prompt placement of HDI on Chuck 2
# 2. Turn on Chuck 2's vacuum
# 3. Move gantry head to Chuck 2 and survey 4 corners of HDI (defined by bond pads)
# 4. Compute would-be position and rotation of HDI on Chuck 1
# 5. Compute the positions of the centers for all four glass pieces
# 6. Prompt placement of the four SensorROC slides on the launchpad
#    i. Is Chuck 1 clear?
#    ii. Is the Pickup Tool ready?
# 7. Survey the four SensorROC's on the launchpad
# 8. Pick up the SensorROC's and place them on Chuck 1 at positions computed in Step 5
# 9. Prompt placement of vacuum tool on HDI on Chuck 2
# 10. Transfer vacuum from Chuck 2 to Vacuum Tool (Chuck 3)
# 11. Check that HDI was moved to Chuck 3 and the adhesive was applied
# 12. Prompt placement of Vacuum Tool bearing HDI on Chuck 1
# 13. Release vacuum of Vacuum Tool
# 14. Prompt replacement of Vacuum Tool on Chuck 3
# 15. Surveillance
# 16. End program with vacuums on or off

#Initialize the system
LOADCONFIG
HOME
SETVAC gantry_head_inner 0
SETVAC gantry_head_outer 0
SETVAC rd53a_chuck_1_TL 0
SETVAC rd53a_chuck_1_TR 0
SETVAC rd53a_chuck_1_BL 0
SETVAC rd53a_chuck_1_BR 0
SETVAC rd53a_chuck_2 0
SETVAC rd53a_chuck_3 0
SETVAC launchpad 0
SETLOG "LOG_Assembly_RD53A_MultiSensor_Data.txt"
SETERRORMODE prompt

# Rough positions of the HDI on Chuck 2
COPY $pos_chuck2_tr {460.391,528.560,100.385}
COPY $pos_chuck2_br {460.430,568.240,100.385}
COPY $pos_chuck2_bl {440.753,568.246,100.385}
COPY $pos_chuck2_tl {440.705,528.560,100.385}

# Rough positions of the tr corner of the 4 SensorROCs on Launchpad
COPY $pos_TRsenRoc_launch_tr {654.324,194.016,88.581}
COPY $pos_BRsenRoc_launch_tr {654.030,219.431,88.569}
COPY $pos_BLsenRoc_launch_tr {633.359,219.069,88.641}
COPY $pos_TLsenRoc_launch_tr {633.636,193.902,88.602}

# Rough positions of the HDI on Chuck 1
COPY $pos_chuck1_tr {410.391,528.560,93.226}
COPY $pos_chuck1_br {410.430,568.240,93.226}
COPY $pos_chuck1_bl {390.753,568.246,93.226}
COPY $pos_chuck1_tl {390.705,528.560,93.226}

# Rough positions of the SensorROC on Chuck 1
COPY $pos_senRoc1_tr {412.500,528.000,93.467}
COPY $pos_senRoc1_br {412.500,569.000,93.467}
COPY $pos_senRoc1_bl {389.000,569.000,93.467}
COPY $pos_senRoc1_tl {389.000,528.000,93.467}

# HDI pick and place translation vector and rotation angle measured independently
COPY $v_hdi_translation {-49.833,0.065,-6.350}
COPY $rot_hdi_translation -0.026

# SensorROC translation correctional terms measured independently
COPY $v_senRoc_error {-0.003,0.007,0}
COPY $rot_senRoc_error -0.007

# SensorROC dimensions
COPY $len_senRoc 20.044 #mm
COPY $wid_senRoc 11.859 #mm
COPY $len2_senRoc 21.714 #mm
COPY $delta_len 0.1 #mm
COPY $delta_wid 0.1 #mm

# SensorROC with bond pad displacement in y to align bond pad with HDI
COPY $delta_alignment 1.005 #mm

# For surveillance: Rough positions of upper and lower bond pads on the HDI and sensorROC
COPY $bondWidth_HDI 0.40
COPY $pos_HDI_upperPad {391.166,528.729,93.206}
COPY $pos_HDI_lowerPad {391.249,548.398,93.043}
COPY $pos_senROC_upperPad {388.708,528.860,93.344}
COPY $pos_senROC_lowerPad {388.782,548.560,93.344}

GOTO @STEP_FIFTEEN

# Step 1. Prompt placement of HDI on Chuck 2
PRINT "--------------------"
@STEP_ONE CHOICEPOPUP $choice "Have you placed the RD53A HDI on Chuck 2?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_ONE $choice

# Step 2. Turn on Chuck 2's vacuum

SETVAC rd53a_chuck_2 1

# Step 3. Move gantry head to Chuck 2 and survey 4 corners of HDI (defined by bond pads)

MOVESAFE $pos_chuck2_tr 100
COPY $x_corner $pos_chuck2_tr
@HDI_0 CHOICEPOPUP $choice "Ready to find the Top Right marker?" "Yes" "No"
GOTOIFN @HDI_0 $choice
CALL @SURVEYCORNERS
COPY $pos_chuck2_tr $x_corner

COPY $x_corner $pos_chuck2_br
@HDI_1 CHOICEPOPUP $choice "Ready to find the Bottom Right marker?" "Yes" "No"
GOTOIFN @HDI_1 $choice
CALL @SURVEYCORNERS
COPY $pos_chuck2_br $x_corner

COPY $x_corner $pos_chuck2_bl
@HDI_2 CHOICEPOPUP $choice "Ready to find the Bottom Left marker?" "Yes" "No"
GOTOIFN @HDI_2 $choice
CALL @SURVEYCORNERS
COPY $pos_chuck2_bl $x_corner

COPY $x_corner $pos_chuck2_tl
@HDI_3 CHOICEPOPUP $choice "Ready to find the Top Left marker?" "Yes" "No"
GOTOIFN @HDI_3 $choice
CALL @SURVEYCORNERS
COPY $pos_chuck2_tl $x_corner

# Step 4. Compute would-be position and rotation of the HDI on Chuck 1

FIT $pos_hdi_center $rot_hdi TFPX_RD53AHDI $pos_chuck2_tr $pos_chuck2_br $pos_chuck2_bl $pos_chuck2_tl

ADD $pos_hdi_center $pos_hdi_center $v_hdi_translation

QUAT2EULER $rot_hdi $trash0 $trash1 $rot_hdi
ADD $rot_hdi $rot_hdi $rot_hdi_translation

ADD $pos_hdi_center_withError $pos_hdi_center $v_senRoc_error
ADD $rot_hdi_withError $rot_hdi $rot_senRoc_error

MOVESAFE {350,150,0} 100

# Step 5. Compute the positions of the centers for all four glass pieces

COS $cos_rot_hdi $rot_hdi_withError
SIN $sin_rot_hdi $rot_hdi_withError

MUL $half_len_senRoc $len_senRoc 0.5
MUL $half_wid_senRoc $wid_senRoc 0.5
ADD $half_len_senRoc $half_len_senRoc $delta_len
ADD $half_wid_senRoc $half_wid_senRoc $delta_wid

MUL $half_len_cos $half_len_senRoc $cos_rot_hdi
MUL $half_wid_sin $half_wid_senRoc $sin_rot_hdi
MUL $half_len_sin $half_len_senRoc $sin_rot_hdi
MUL $half_wid_cos $half_wid_senRoc $cos_rot_hdi

#TR
ADD $delta_x $half_len_sin $half_wid_cos
SUB $delta_y $half_wid_sin $half_len_cos
ADD $pos_TRsenRoc_center $pos_hdi_center_withError.x $delta_x
ADD $pos_TRsenRoc_center.y $pos_hdi_center_withError.y $delta_y

#BR
SUB $delta_x $half_wid_cos $half_len_sin
ADD $delta_y $half_len_cos $half_wid_sin
ADD $pos_BRsenRoc_center $pos_hdi_center_withError.x $delta_x
ADD $pos_BRsenRoc_center.y $pos_hdi_center_withError.y $delta_y

#BL
ADD $half_len3_senRoc $half_len_senRoc $delta_alignment
MUL $half_len3_cos $half_len3_senRoc $cos_rot_hdi
MUL $half_len3_sin $half_len3_senRoc $sin_rot_hdi
ADD $delta_x $half_len3_sin $half_wid_cos
SUB $delta_y $half_len3_cos $half_wid_sin
SUB $pos_BLsenRoc_center $pos_hdi_center_withError.x $delta_x
ADD $pos_BLsenRoc_center.y $pos_hdi_center_withError.y $delta_y

#TL
MUL $half_len2_senRoc $len2_senRoc 0.5
ADD $half_len2_senRoc $half_len2_senRoc $delta_len
SUB $half_len2_senRoc $half_len2_senRoc $delta_alignment
MUL $half_len2_cos $half_len2_senRoc $cos_rot_hdi
MUL $half_len2_sin $half_len2_senRoc $sin_rot_hdi
SUB $delta_x $half_len2_sin $half_wid_cos
ADD $delta_y $half_len2_cos $half_wid_sin
ADD $pos_TLsenRoc_center $pos_hdi_center_withError.x $delta_x
SUB $pos_TLsenRoc_center.y $pos_hdi_center_withError.y $delta_y

#Copying the z-components
COPY $pos_TLsenRoc_center.z $pos_hdi_center_withError.z
COPY $pos_BRsenRoc_center.z $pos_hdi_center_withError.z
COPY $pos_BLsenRoc_center.z $pos_hdi_center_withError.z
COPY $pos_TRsenRoc_center.z $pos_hdi_center_withError.z

# Step 6. Prompt placement of the four SensorROC slides on the launchpad

@STEP_SIX CHOICEPOPUP $choice "Have you placed the SensorROC on the launchpad?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_SIX $choice
SETVAC launchpad 1
@STEP_SIX_i CHOICEPOPUP $choice "Is Chuck 1 clear?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_SIX_i $choice
@STEP_SIX_ii CHOICEPOPUP $choice "Is pickup tool ready?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_SIX_ii $choice

# Step 7. Survey the four SensorROC's on the launchpad

  #TR Sensor
COPY $launch_tr $pos_TRsenRoc_launch_tr
CALL @FINDSENSORCENTERS
COPY $pos_TRsenRoc_launch $pos_senRoc_center
COPY $rot_TRsenRoc_launch $rot_senRoc

  #BR Sensor
COPY $launch_tr $pos_BRsenRoc_launch_tr
CALL @FINDSENSORCENTERS
COPY $pos_BRsenRoc_launch $pos_senRoc_center
COPY $rot_BRsenRoc_launch $rot_senRoc

  #BL Sensor
COPY $launch_tr $pos_BLsenRoc_launch_tr
CALL @FINDSENSORCENTERS
COPY $pos_BLsenRoc_launch $pos_senRoc_center
COPY $rot_BLsenRoc_launch $rot_senRoc

  #TL Sensor
COPY $launch_tr $pos_TLsenRoc_launch_tr
CALL @FINDSENSORCENTERS
FIT $pos_senRoc_center $rot_senRoc TFPX_RD53ABONDEDQSENSORROC $launch_tr $launch_br $launch_bl $launch_tl
QUAT2EULER $rot_senRoc $trash0 $trash1 $rot_senRoc
COPY $pos_TLsenRoc_launch $pos_senRoc_center
COPY $rot_TLsenRoc_launch $rot_senRoc

# Step 8. Pick up the SensorROC's and place them on Chuck 1 at positions computed in Step 5
@STEP_EIGHT PASS
LOADTOOL picker_tool
MOVENAME stagingArea

##################
# Pickup Step TR #
COPY $target_pos $pos_TRsenRoc_launch
COPY $target_rot $rot_TRsenRoc_launch
CALL @CALCPICKUP_4SENSORS
CALL @PICKUPSTEP

# Place Step TR
COPY $target_pos $pos_TRsenRoc_center
COPY $target_rot $rot_hdi_withError
CALL @CALCPICKUP_4SENSORS
COPY $vacIndex @TURNON_chuck1_TR
CALL @PLACESTEP

##################
# Pickup Step BR #
COPY $target_pos $pos_BRsenRoc_launch
copy $target_rot $rot_BRsenRoc_launch
CALL @CALCPICKUP_4SENSORS
CALL @PICKUPSTEP

# Place Step BR
COPY $target_pos $pos_BRsenRoc_center
COPY $target_rot $rot_hdi_withError
CALL @CALCPICKUP_4SENSORS
COPY $vacIndex @TURNON_chuck1_BR
CALL @PLACESTEP

##################
# Pickup Step BL #
COPY $target_pos $pos_BLsenRoc_launch
copy $target_rot $rot_BLsenRoc_launch
CALL @CALCPICKUP_4SENSORS
CALL @PICKUPSTEP

# Place Step BL
COPY $target_pos $pos_BLsenRoc_center
COPY $target_rot $rot_hdi_withError
CALL @CALCPICKUP_4SENSORS
COPY $vacIndex @TURNON_chuck1_BL
CALL @PLACESTEP

##################
# Pickup Step TL #
COPY $target_pos $pos_TLsenRoc_launch
copy $target_rot $rot_TLsenRoc_launch
CALL @CALCPICKUP_4SENSORS
CALL @PICKUPSTEP

# Place Step TL
COPY $target_pos $pos_TLsenRoc_center
COPY $target_rot $rot_hdi_withError
CALL @CALCPICKUP_4SENSORS
COPY $vacIndex @TURNON_chuck1_TL
CALL @PLACESTEP

ROTATETO 0 10
UNLOADTOOL

MOVESAFE {350,150,0} 100

# Step 9. Prompt placement of vacuum tool on HDI on Chuck 2
@STEP_NINE CHOICEPOPUP $choice "Have you placed the Vacuum Tool on the HDI in Chuck 2 such that the fiducials are visible through its viewing holes?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_NINE $choice

# Step 10. Transfer vacuum from Chuck 2 to Vacuum Tool (Chuck 3)

SETVAC rd53a_chuck_3 1
WAIT 1000
SETVAC rd53a_chuck_2 0

# Step 11. Check that HDI was moved to Chuck 3 and the adhesive was applied
@STEP_ELEVEN CHOICEPOPUP $choice "Did you move the HDI to Chuck 3 and apply the adhesive?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_ELEVEN $choice

# Step 12. Prompt placement of Vacuum Tool bearing HDI on Chuck 1
@STEP_TWELVE CHOICEPOPUP $choice "Did you place the HDI on the SensorROC on Chuck 1 (With adhesive between them)?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_TWELVE $choice

# Step 13. Release vacuum of Vacuum Tool

SETVAC rd53a_chuck_3 0

# Step 14. Prompt replacement of Vacuum Tool on Chuck 3
@STEP_FOURTEEN CHOICEPOPUP $choice "Did you place the Vacuum Tool on Chuck 3?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_FOURTEEN $choice

# Step 15. Surveillance
@STEP_FIFTEEN PASS

@SURV_HDI_UPPER_LEFT CHOICEPOPUP $choice "Ready to find the left-most point of the HDI upper bond pad?" "Yes" "No"
GOTOIFN @SURV_HDI_UPPER_LEFT $choice
SUB $x_corner $pos_HDI_upperPad $bondWidth_HDI
CALL @SURVEYCORNERS
COPY $pos_HDI_upperPad_left $x_corner

@SURV_HDI_UPPER_RIGHT CHOICEPOPUP $choice "Ready to find the right-most point of the HDI upper bond pad?" "Yes" "No"
GOTOIFN @SURV_HDI_UPPER_RIGHT $choice
ADD $x_corner $pos_HDI_upperPad $bondWidth_HDI
CALL @SURVEYCORNERS
COPY $pos_HDI_upperPad_right $x_corner

@SURV_HDI_LOWER_LEFT CHOICEPOPUP $choice "Ready to find the left-most point of the HDI lower bond pad?" "Yes" "No"
GOTOIFN @SURV_HDI_LOWER_LEFT $choice
SUB $x_corner $pos_HDI_lowerPad $bondWidth_HDI
CALL @SURVEYCORNERS
COPY $pos_HDI_lowerPad_left $x_corner

@SURV_HDI_LOWER_RIGHT CHOICEPOPUP $choice "Ready to find the right-most point of the HDI lower bond pad?" "Yes" "No"
GOTOIFN @SURV_HDI_LOWER_RIGHT $choice
ADD $x_corner $pos_HDI_lowerPad $bondWidth_HDI
CALL @SURVEYCORNERS
COPY $pos_HDI_lowerPad_right $x_corner

@SURV_SENROC_UPPER CHOICEPOPUP $choice "Ready to find the center of the SensorROC upper bond pad?" "Yes" "No"
GOTOIFN @SURV_SENROC_UPPER $choice
COPY $x_corner $pos_senROC_upperPad
CALL @SURVEYCORNERS
COPY $pos_senROC_upperPad $x_corner

@SURV_SENROC_LOWER CHOICEPOPUP $choice "Ready to find the center of the SensorROC lower bond pad?" "Yes" "No"
GOTOIFN @SURV_SENROC_LOWER $choice
COPY $x_corner $pos_senROC_lowerPad
CALL @SURVEYCORNERS
COPY $pos_senROC_lowerPad $x_corner

PRINT "Surveillance:"
PRINT "HDI upper bond pad left: %v" $pos_HDI_upperPad_left
PRINT "HDI upper bond pad right: %v" $pos_HDI_upperPad_right
PRINT "HDI lower bond pad left: %v" $pos_HDI_lowerPad_left
PRINT "HDI lower bond pad right: %v" $pos_HDI_lowerPad_right
PRINT "SensorROC upper bond pad center: %v" $pos_senROC_upperPad
PRINT "SensorROC lower bond pad center: %v" $pos_senROC_lowerPad
PRINT " === "

# Remove these rough calculations once you have the python script
ADD $pos_HDI_upperPad $pos_HDI_upperPad_left $pos_HDI_upperPad_right
MUL $pos_HDI_upperPad $pos_HDI_upperPad {0.5,0.5,0.5}
ADD $pos_HDI_lowerPad $pos_HDI_lowerPad_left $pos_HDI_lowerPad_right
MUL $pos_HDI_lowerPad $pos_HDI_lowerPad {0.5,0.5,0.5}
SUB $delta_Upper $pos_HDI_upperPad $pos_senROC_upperPad
SUB $delta_Lower $pos_HDI_lowerPad $pos_senROC_lowerPad
SUB $deltaDeltaX $delta_Upper.x $delta_Lower.x
ADD $avgDeltaY $delta_Upper.y $delta_Lower.y
MUL $avgDeltaY $avgDeltaY 0.5
PRINT "Delta(delta(X)): %f" $deltaDeltaX
PRINT "Avg(delta(Y)): %f" $avgDeltaY


# Step 16. End program with vacuums on or off
CHOICEPOPUP $choice "Do you want to leave the vacuums on?" "Yes, Leave them on" "No, Turn them off"
GOTOIF @STEP_SIXTEEN $choice

SETVAC gantry_head_inner 0
SETVAC gantry_head_outer 0
SETVAC rd53a_chuck_1_TL 0
SETVAC rd53a_chuck_1_TR 0
SETVAC rd53a_chuck_1_BL 0
SETVAC rd53a_chuck_1_BR 0
SETVAC rd53a_chuck_2 0
SETVAC rd53a_chuck_3 0
SETVAC launchpad 0

@STEP_SIXTEEN PASS
HOME

END

#FINDSENSORCENTERS
# Arguments
# $launch_tr
@FINDSENSORCENTERS COPY $RET_FINDSENSORCENTERS $RET
  MOVESAFE $launch_tr 50
  COPY $x_corner $launch_tr
  @ROC_0 CHOICEPOPUP $choice "Ready to find the Top Right marker?" "Yes" "No"
  GOTOIFN @ROC_0 $choice
  CALL @SURVEYCORNERS
  COPY $launch_tr $x_corner

  COPY $launch_br $launch_tr
  ADD $launch_br.y $launch_tr.y $len_senRoc
  COPY $x_corner $launch_br
  @ROC_1 CHOICEPOPUP $choice "Ready to find the Bottom Right marker?" "Yes" "No"
  GOTOIFN @ROC_1 $choice
  CALL @SURVEYCORNERS
  COPY $launch_br $x_corner

  COPY $launch_bl $launch_br
  SUB $launch_bl.x $launch_bl.x $wid_senRoc
  COPY $x_corner $launch_bl
  @ROC_2 CHOICEPOPUP $choice "Ready to find the Bottom Left marker?" "Yes" "No"
  GOTOIFN @ROC_2 $choice
  CALL @SURVEYCORNERS
  COPY $launch_bl $x_corner

  COPY $launch_tl $launch_tr
  SUB $launch_tl.x $launch_tl.x $wid_senRoc
  COPY $x_corner $launch_tl
  @ROC_3 CHOICEPOPUP $choice "Ready to find the Top Left marker?" "Yes" "No"
  GOTOIFN @ROC_3 $choice
  CALL @SURVEYCORNERS
  COPY $launch_tl $x_corner

  # Calculate the orientation of the SensorROC
  FIT $pos_senRoc_center $rot_senRoc TFPX_RD53AQUARTERSENSORROC $launch_tr $launch_br $launch_bl $launch_tl

  QUAT2EULER $rot_senRoc $trash0 $trash1 $rot_senRoc

  PRINT "Sen Roc"
  PRINT "Rot: %f Center: %v" $rot_senRoc $pos_senRoc_center
  PRINT "HDI"
  PRINT "Rot: %f Center: %v" $rot_hdi $pos_hdi_center
  PRINT ""

  CHOICEPOPUP $choice "Are angles and positions reasonable (See console)?" "Yes, continue" "No"
  GOTOIF @ANGLE_CHECK $choice
  END
  @ANGLE_CHECK
  GOTO $RET_FINDSENSORCENTERS


# SURVEYCORNERS
# Arguments
# $x_corner - Approximate position of an object that you would like to know the precise position. Returns with precise position written in this location
@SURVEYCORNERS COPY $RET_SURVEYCORNERS $RET
  MOVESAFE $x_corner 50
  @SURCORNPOP VIDEO
  CHOICEPOPUP $choice "Did you correctly align the cursor with the marker?" "Yes" "No"
  GOTOIFN @SURCORNPOP $choice
  GETPOS $x_corner
  GOTO $RET_SURVEYCORNERS

# CALCPICKUP_4SENSORS
# Arguments:
#  $target_pos - center of piece to pickup/place
#  $target_rot - rotation of piece to pickup/place
@CALCPICKUP_4SENSORS COPY $RET_CALCPICKUP_4SENSORS $RET
  FLEXREAD $picker_height geometry.PICKER_TOOL.center_offset
  FLEXREAD $ghco geometry.tool_holder_offset
  ADD $netoffset $picker_height $ghco
  SUB $pickup_pos $target_pos $netoffset
  COPY $pickup_pos_z0 $pickup_pos
  COPY $pickup_pos_z0.z 0
  # X & Y Correction
  SIN $sin_theta $target_rot
  COS $cos_theta $target_rot
  FLEXREAD $center2topSuction geometry.PICKER_TOOL.center_to_suction
  MUL $x_correction $center2topSuction.y $sin_theta
  MUL $y_correction $center2topSuction.y $cos_theta
  SUB $pickup_pos.x $pickup_pos.x $x_correction
  ADD $pickup_pos.y $pickup_pos.y $y_correction
  SUB $pickup_pos_z0.x $pickup_pos_z0.x $x_correction
  ADD $pickup_pos_z0.y $pickup_pos_z0.y $y_correction
  COPY $total_rot $target_rot
  #Rotation + 90 degrees
  #ADD $total_rot $target_rot 90
  GOTO $RET_CALCPICKUP_4SENSORS

# PICKUPSTEP
# takes place directly after CALCPICKUP_4SENSORS
# Arguments:
# variables from CALCPICKUP_4SENSORS
@PICKUPSTEP COPY $RET_PICKUPSTEP $RET
  MOVETO $pickup_pos_z0 100
  ROTATETO $total_rot 10
  SUB $pickup_pos.z $pickup_pos.z 5
  MOVETO $pickup_pos 10
  ADD $pickup_pos.z $pickup_pos.z 5
  MOVETO $pickup_pos 1
  WAIT 500
  SETVAC gantry_head_inner 1
  WAIT 2000
  SETVAC launchpad 0
  WAIT 1000
  MOVETO $pickup_pos_z0 10
#  ROTATETO 0 10
  GOTO $RET_PICKUPSTEP

# PLACESTEP
# takes place directly after CALCPICKUP_4SENSORS
# Arguments:
#  Variables from CALCPICKUP_4SENSORS
#  Vacuum index: $vacIndex = {TURNONVAC0, TURNONVAC1, TURNONVAC2, TURNONVAC3}
@PLACESTEP COPY $RET_PLACESTEP $RET
  MOVETO $pickup_pos_z0 50
  ROTATETO $total_rot 10
  SUB $pickup_pos.z $pickup_pos.z 5
  MOVETO $pickup_pos 10
  ADD $pickup_pos.z $pickup_pos.z 5
  MOVETO $pickup_pos 1
  WAIT 500
  GOTO $vacIndex
  @TURNON_chuck1_TR SETVAC rd53a_chuck_1_TR 1
  GOTO @VACWAIT
  @TURNON_chuck1_BR SETVAC rd53a_chuck_1_BR 1
  GOTO @VACWAIT
  @TURNON_chuck1_BL SETVAC rd53a_chuck_1_BL 1
  GOTO @VACWAIT
  @TURNON_chuck1_TL SETVAC rd53a_chuck_1_TL 1
  @VACWAIT
  WAIT 1000
  SETVAC gantry_head_inner 0
  WAIT 2000
  MOVETO $pickup_pos_z0 10
#  ROTATETO 0 10
  GOTO $RET_PLACESTEP
