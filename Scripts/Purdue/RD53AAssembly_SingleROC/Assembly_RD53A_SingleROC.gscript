VERSION v2.1
######################## RD53A Single Sensor ROC Assembly #############################
# Steps:
# 1. Prompt placement of HDI on Chuck 2
# 2. Turn on Chuck 2's vacuum
# 3. Move gantry head to Chuck 2 and survey 4 corners of HDI (defined by bond pads)
# 4. Compute would-be position and rotation of HDI on Chuck 1
# 5. Compute the position of the center of the ROC
# 6. Prompt placement of the ROC on the launchpad
#    i. Is Chuck 1 clear ?
#    ii. Is the Pickup Tool ready?
# 7. Survey the ROC on the launchpad
# 8. Pick up the ROC and place it on Chuck 1 at the position computed in Step 5
# 9. Prompt placement of vacuum tool on HDI on Chuck 2
# 10. Transfer vacuum from Chuck 2 to Vacuum Tool (Chuck 3)
# 11. Check that HDI was moved to Chuck 3 and the adhesive was applied
# 12. Prompt placement of Vacuum Tool bearing HDI on Chuck 1
# 13. Release vacuum of Vacuum Tool
# 14. Prompt replacement of Vacuum Tool on Chuck 3
# 15. Calculate the placement of the HDI and the ROC
# 16. Move gantry head to Chuck 1 and survey for Module accuracy
# 17. End program with vacuums on or off

#Initialize the system
LOADCONFIG
HOME
SETVAC gantry_head_inner 0
SETVAC gantry_head_outer 0
SETVAC rd53a_chuck_1_TL 0
SETVAC rd53a_chuck_1_TR 0
SETVAC rd53a_chuck_1_BL 0
SETVAC rd53a_chuck_1_BR 0
SETVAC rd53a_chuck_2 0
SETVAC rd53a_chuck_3 0
#SETVAC launchpad2_TR 0
#SETVAC launchpad2_BR 0
#SETVAC launchpad2_BL 0
#SETVAC launchpad2_TL 0
SETLOG "LOG_Assembly_RD53A_SingleROC_DataGeneration.txt"
SETERRORMODE prompt

# Rough positions of the HDI on Chuck 2
FLEXREAD $pos_chuck2_tr siteGeometry.pos_chuck2_tr
FLEXREAD $pos_chuck2_br siteGeometry.pos_chuck2_br
FLEXREAD $pos_chuck2_bl siteGeometry.pos_chuck2_bl
FLEXREAD $pos_chuck2_tl siteGeometry.pos_chuck2_tl

# Rough positions of the tr corner of the ROC on Launchpad 2
FLEXREAD $pos_ROC_launch1_tr siteGeometry.pos_ROC_launch1_tr


# Dimensions of the HDI and ROC (bond pads or corners)
FLEXREAD $thickness_HDI geometry.TFPX_RD53AHDI.thickness
FLEXREAD $dim_bondHDI geometry.TFPX_RD53AHDI.dim_bondHDI
#MUL $dim_bondHDI $dim_bondHDI {0.5,0.5,1}
FLEXREAD $len_bondpad_HDI geometry.TFPX_RD53AHDI.len_bondpad_HDI

FLEXREAD $dim_ROC geometry.TFPX_RD53AROC.fid_br
FLEXREAD $dim_ROC.z geometry.TFPX_RD53AROC.thickness

# HDI pick and place translation vector and rotation angle measured independently
# Correct for the thickness of the ROC
FLEXREAD $v_hdi_translation siteGeometry.v_hdi_translation
SUB $v_hdi_translation.z $v_hdi_translation.z $dim_ROC.z
FLEXREAD $rot_hdi_translation siteGeometry.rot_hdi_translation

# ROC translation correctional terms measured independently
FLEXREAD $v_ROC_error siteGeometry.v_ROC_error
FLEXREAD $rot_ROC_error siteGeometry.rot_ROC_error

# ROC dimensions
MUL $len_ROC $dim_ROC.y 2 #mm
MUL $wid_ROC $dim_ROC.x 2 #mm

# Parameters that can help line up the bond pads
FLEXREAD $delta_ROC siteGeometry.delta_ROC



## Main Function ##
# Step 1. Prompt placement of HDI on Chuck 2
PRINT "--------------------"
@STEP_ONE CHOICEPOPUP $choice "Have you placed the RD53A HDI on Chuck 2?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_ONE $choice


# Step 2. Turn on Chuck 2's vacuum
@STEP_TWO PASS
SETVAC rd53a_chuck_2 1


# Step 3. Move gantry head to Chuck 2 and survey 4 corners of HDI (defined by bond pads)
@STEP_THREE PASS
MOVESAFE $pos_chuck2_tr 100
@HDI_0 CHOICEPOPUP $choice "Ready to find the top-right corner of the Top Right bond pad?" "Yes" "No"
GOTOIFN @HDI_0 $choice
CALL @SURVEYCORNERS $pos_chuck2_tr -> $pos_chuck2_tr

@HDI_1 CHOICEPOPUP $choice "Ready to find the bottom-right corner of the Bottom Right bond pad?" "Yes" "No"
GOTOIFN @HDI_1 $choice
CALL @SURVEYCORNERS $pos_chuck2_br -> $pos_chuck2_br

@HDI_2 CHOICEPOPUP $choice "Ready to find the bottom-left corner of the Bottom Left bond pad?" "Yes" "No"
GOTOIFN @HDI_2 $choice
CALL @SURVEYCORNERS $pos_chuck2_bl -> $pos_chuck2_bl

@HDI_3 CHOICEPOPUP $choice "Ready to find the top-left corner of the Top Left bond pad?" "Yes" "No"
GOTOIFN @HDI_3 $choice
CALL @SURVEYCORNERS $pos_chuck2_tl -> $pos_chuck2_tl


# Step 4. Compute would-be position and rotation of the HDI on Chuck 1
@STEP_FOUR PASS
FIT $pos_hdi_center $rot_hdi TFPX_RD53AHDI $pos_chuck2_tr $pos_chuck2_br $pos_chuck2_bl $pos_chuck2_tl

ADD $pos_hdi_center $pos_hdi_center $v_hdi_translation

QUAT2EULER $rot_hdi $trash0 $trash1 $rot_hdi
ADD $rot_hdi $rot_hdi $rot_hdi_translation

ADD $pos_hdi_center_withError $pos_hdi_center $v_ROC_error
ADD $rot_hdi_withError $rot_hdi $rot_ROC_error

PRINT "HDI"
PRINT "Rot: %f Center: %v" $rot_hdi_withError $pos_hdi_center_withError
PRINT ""

CHOICEPOPUP $choice "Are angles and positions reasonable (See console)?" "Yes, continue" "No"
GOTOIF @ANGLE_CHECK $choice
END
@ANGLE_CHECK

MOVENAME safeUnload


# Step 5. Compute the position of the center of the ROC
@STEP_FIVE PASS

COS $cos_rot_hdi $rot_hdi_withError
SIN $sin_rot_hdi $rot_hdi_withError

MUL $dx_cos $delta_ROC.x $cos_rot_hdi
MUL $dx_sin $delta_ROC.x $sin_rot_hdi
MUL $dy_cos $delta_ROC.y $cos_rot_hdi
MUL $dy_sin $delta_ROC.y $sin_rot_hdi

SUB $delta_x $dx_cos $dy_sin
ADD $delta_y $dx_sin $dy_cos
ADD $pos_ROC_center $pos_hdi_center_withError.x $delta_x
ADD $pos_ROC_center.y $pos_hdi_center_withError.y $delta_y
COPY $pos_ROC_center.z $pos_hdi_center_withError.z


# Step 6. Prompt placement of ROC
SETVAC gelpak_release 1
@STEP_SIX CHOICEPOPUP $choice "Have you placed the ROC on the Launchpad?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_SIX $choice
SETVAC gelpak_release 0
@STEP_SIX_i CHOICEPOPUP $choice "Is Chuck 1 clear?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_SIX_i $choice
@STEP_SIX_ii CHOICEPOPUP $choice "Is the Pickup Tool ready?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_SIX_ii $choice


# Step 7. Survey the ROC
@STEP_SEVEN PASS
CALL @FINDROCCENTERS $pos_ROC_launch1_tr $len_ROC $wid_ROC -> $pos_ROC_launch1 $rot_ROC_launch1



# Step 8. Pick up the ROCs and place them on Chuck 1 at positions computed in Step 5
@STEP_EIGHT PASS
LOADTOOL picker_tool
MOVENAME stagingArea

####################
  # Pickup Step TR #
CALL @CALCPICKUP_4ROC $pos_ROC_launch1 $rot_ROC_launch1 -> $pos $pos_z0
CALL @PICKUPSTEP $pos $pos_z0 $rot_ROC_launch1

  # Place Step TR
CALL @CALCPICKUP_4ROC $pos_ROC_center $rot_hdi_withError -> $pos $pos_z0
CALL @PLACESTEP $pos $pos_z0 $rot_hdi_withError

ROTATETO 0 10
UNLOADTOOL

MOVENAME safeUnload


# Step 9. Prompt placement of vacuum tool on HDI on Chuck 2
@STEP_NINE CHOICEPOPUP $choice "Have you placed the Vacuum Tool on the HDI in Chuck 2 such that the fiducials are visible through its viewing holes?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_NINE $choice


# Step 10. Transfer vacuum from Chuck 2 to Vacuum Tool (Chuck 3)
@STEP_TEN PASS
SETVAC rd53a_chuck_3 1
WAIT 1000
SETVAC rd53a_chuck_2 0


# Step 11. Check that HDI was moved to Chuck 3 and the adhesive was applied
@STEP_ELEVEN CHOICEPOPUP $choice "Did you move the HDI to Chuck 3 and apply the adhesive?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_ELEVEN $choice


# Step 12. Prompt placement of Vacuum Tool bearing HDI on Chuck 1
@STEP_TWELVE CHOICEPOPUP $choice "Did you place the HDI on the ROC on Chuck 1 (With adhesive between them)?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_TWELVE $choice


# Step 13. Release vacuum of Vacuum Tool
@STEP_THIRTEEN PASS
SETVAC rd53a_chuck_3 0


# Step 14. Prompt replacement of Vacuum Tool on Chuck 3
@STEP_FOURTEEN CHOICEPOPUP $choice "Did you place the Vacuum Tool on Chuck 3?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_FOURTEEN $choice


# Step 15. Calculate the placement of the ROC and the HDI
@STEP_FIFTEEN PASS
  #ROC
CALL @CALCCORNERS $pos_ROC_center $rot_hdi_withError $dim_ROC -> $pos_ROC_tr $pos_ROC_br $pos_ROC_bl $pos_ROC_tl


  #HDI
SUB $pos_hdi_center_withError.z $pos_hdi_center_withError.z $thickness_HDI

CALL @CALCCORNERS $pos_hdi_center_withError $rot_hdi_withError $dim_bondHDI -> $pos_chuck1_tr $pos_chuck1_br $pos_chuck1_bl $pos_chuck1_tl


# Step 16. Move gantry head to Chuck 1 and survey for Module accuracy
@STEP_SIXTEEN PASS
@ACC_0 CHOICEPOPUP $choice "Ready to survey the right side of the Module's accuracy?"
GOTOIFN @ACC_0 $choice
CALL @FULLSURVEILLENCE $pos_chuck1_tr $pos_chuck1_br $pos_ROC_tr $pos_ROC_br -1 -> $right_dx $right_dy

@ACC_1 CHOICEPOPUP $choice "Ready to survey the left side of the Module's accuracy?"
GOTOIFN @ACC_1 $choice
CALL @FULLSURVEILLENCE $pos_chuck1_tl $pos_chuck1_bl $pos_ROC_tl $pos_ROC_bl +1 -> $left_dx $left_dy


PRINT "---------------------"
PRINT "Right Side"
PRINT "Delta(delta(X)): %f" $right_dx
PRINT "Avg(delta(Y)): %f" $right_dy

PRINT "---------------------"
PRINT "Left Side"
PRINT "Delta(delta(X)): %f" $left_dx
PRINT "Avg(delta(Y)): %f" $left_dy


# Step 17. End program with vacuums on or off
@STEP_SEVENTEEN PASS
CHOICEPOPUP $choice "Do you want to leave the vacuums on?" "Yes, Leave them on" "No, Turn them off"
GOTOIF @STEP_END $choice

SETVAC gantry_head_inner 0
SETVAC gantry_head_outer 0
SETVAC rd53a_chuck_1_TL 0
SETVAC rd53a_chuck_1_TR 0
SETVAC rd53a_chuck_1_BL 0
SETVAC rd53a_chuck_1_BR 0
SETVAC rd53a_chuck_2 0
SETVAC rd53a_chuck_3 0
SETVAC launchpad 0

@STEP_END HOME

END



# SURVEYCORNERS
# Arguments:
# $x_corner - Approximate position of an object that you would like to know the precise position. Returns with precise position written in this location
#
# Returns:
# $x_corner - Updated position of the corner that is being looked for
@SURVEYCORNERS(x_corner)
  MOVESAFE $x_corner 100
  @SURCORNPOP VIDEO
  CHOICEPOPUP $choice "Did you correctly align the cursor with the marker?" "Yes" "No"
  GOTOIFN @SURCORNPOP $choice
  GETPOS $x_corner
  RETURN $x_corner


#FINDROCCENTERS
# Arguments:
#  $tr - approximate position of the top right corner
#  $len - length of what is being surveyed
#  $wid - wid of what is being surveyed
#
# Returns:
#  $position - calculated position of the center based on the corners
#  $rotation - calculated rotation based on the corners
@FINDROCCENTERS(tr,len,wid)
  MOVESAFE $tr 100
  @ROC_0 CHOICEPOPUP $choice "Ready to find the Top Right marker?" "Yes" "No"
  GOTOIFN @ROC_0 $choice
  CALL @SURVEYCORNERS $tr -> $tr

  COPY $br $tr
  ADD $br.y $tr.y $len
  @ROC_1 CHOICEPOPUP $choice "Ready to find the Bottom Right marker?" "Yes" "No"
  GOTOIFN @ROC_1 $choice
  CALL @SURVEYCORNERS $br -> $br

  COPY $bl $br
  SUB $bl.x $bl.x $wid
  @ROC_2 CHOICEPOPUP $choice "Ready to find the Bottom Left marker?" "Yes" "No"
  GOTOIFN @ROC_2 $choice
  CALL @SURVEYCORNERS $bl -> $bl

  COPY $tl $tr
  SUB $tl.x $tl.x $wid
  @ROC_3 CHOICEPOPUP $choice "Ready to find the Top Left marker?" "Yes" "No"
  GOTOIFN @ROC_3 $choice
  CALL @SURVEYCORNERS $tl -> $tl

  # Calculate the orientation of the ROC
  FIT $position $rotation TFPX_RD53AROC $tr $br $bl $tl

  QUAT2EULER $rotation $trash0 $trash1 $rotation

  PRINT "ROC"
  PRINT "Rot: %f Center: %v" $rotation $position
  PRINT ""

  CHOICEPOPUP $choice "Are angles and positions reasonable (See console)?" "Yes, continue" "No"
  GOTOIF @ANGLE_CHECK_2 $choice
  END
  @ANGLE_CHECK_2
  RETURN $position $rotation


# CALCPICKUP_4ROC
# Arguments:
#  $target_pos - center of piece to pickup/place
#  $target_rot - rotation of piece to pickup/place
#
# Returns:
#  $pickup_pos - pickup position
#  $pickup_pos_z0 - pickup position with z = 0
@CALCPICKUP_4ROC(target_pos,target_rot)
  FLEXREAD $picker_height geometry.PICKER_TOOL.center_offset
  FLEXREAD $ghco geometry.tool_holder_offset
  ADD $netoffset $picker_height $ghco
  SUB $pickup_pos $target_pos $netoffset
  COPY $pickup_pos_z0 $pickup_pos
  COPY $pickup_pos_z0.z 0
  RETURN $pickup_pos $pickup_pos_z0


# PICKUPSTEP
# takes place directly after CALCPICKUP_4ROC
# Arguments:
#  $pick_pos - pickup position
#  $pick_pos_z0 - pickup position with z = 0
#  $pick_rot - rotation of the what is being picked up
#
# No Returns
@PICKUPSTEP(pick_pos,pick_pos_z0,pick_rot)
  MOVETO $pick_pos_z0 200
  ROTATETO $pick_rot 10
  SUB $pick_pos.z $pick_pos.z 2
  MOVETO $pick_pos 50
  ADD $pick_pos.z $pick_pos.z 2
  MOVETO $pick_pos 1
  WAIT 500
  SETVAC gantry_head_inner 1
  WAIT 2000
  #SETVAC launchpad 0
  WAIT 1000
  SUB $pick_pos.z $pick_pos.z 2
  MOVETO $pick_pos 1
  MOVETO $pick_pos_z0 50
  RETURN


# PLACESTEP
# takes place directly after CALCPICKUP_4ROC
# Arguments:
#  $place_pos - pickup position
#  $place_pos_z0 - pickup position with z = 0
#  $place_rot - rotation of the what is being picked up
#  $vacIndex - current vacuum that needs to be turned on (passed as number)
#
# No Returns
@PLACESTEP(place_pos,place_pos_z0,place_rot)
  MOVETO $place_pos_z0 200
  ROTATETO $place_rot 10
  SUB $place_pos.z $place_pos.z 2
  MOVETO $place_pos 50
  ADD $place_pos.z $place_pos.z 2
  MOVETO $place_pos 1
  WAIT 500
  SETVAC rd53a_chuck_1_TR 1
  SETVAC rd53a_chuck_1_BR 1
  SETVAC rd53a_chuck_1_BL 1
  SETVAC rd53a_chuck_1_TL 1
  WAIT 1000
  SETVAC gantry_head_inner 0
  WAIT 2000
  SUB $place_pos.z $place_pos.z 2
  MOVETO $place_pos 1
  MOVETO $place_pos_z0 50
  RETURN


# CALCCORNERS
# Arguments:
#  $center - center of object
#  $rotation - rotation of the object
#  $dimensions - dimensions of object (as a vector)
#
# Returns
#  $topRight - position of the top right corner
#  $botRight - position of the bottom right corner
#  $botLeft - position of the bottom left corner
#  $topLeft - position of the top left corner
@CALCCORNERS(center,rotation,dimensions)
  SIN $sin_angle $rotation
  COS $cos_angle $rotation
  COPY $width $dimensions.x
  COPY $length $dimensions.y
  MUL $w_cos $width $cos_angle
  MUL $w_sin $width $sin_angle
  MUL $l_cos $length $cos_angle
  MUL $l_sin $length $sin_angle

  ADD $d_x_TR $l_sin $w_cos
  ADD $topRight $center.x $d_x_TR
  SUB $d_y_TR $w_sin $l_cos
  ADD $topRight.y $center.y $d_y_TR
  COPY $topRight.z $center.z

  SUB $d_x_BR $w_cos $l_sin
  ADD $botRight $center.x $d_x_BR
  ADD $d_y_BR $w_sin $l_cos
  ADD $botRight.y $center.y $d_y_BR
  COPY $botRight.z $center.z

  ADD $d_x_BL $w_cos $l_sin
  SUB $botLeft $center.x $d_x_BL
  SUB $d_y_BL $l_cos $w_sin
  ADD $botLeft.y $center.y $d_y_BL
  COPY $botLeft.z $center.z

  SUB $d_x_TL $l_sin $w_cos
  ADD $topLeft $center.x $d_x_TL
  ADD $d_y_TL $l_cos $w_sin
  SUB $topLeft.y $center.y $d_y_TL
  COPY $topLeft.z $center.z

  RETURN $topRight $botRight $botLeft $topLeft


# FULLSURVEILLENCE
# Arguments:
#  $top_hdi - Position of the top HDI bond pad
#  $bot_hdi - Position of the bottom HDI bond pad
#  $top_ROC - Position of the top ROC bond pad
#  $bot_ROC - Position of the bottom ROC bond pad
#  $sign    - +1 for left side of the module, -1 for right side module
#
# Returns:
#  $d_DeltaX   - opening angle
#  $avg_DeltaY - slide
@FULLSURVEILLENCE(top_hdi,bot_hdi,top_ROC,bot_ROC,sign)
  MOVESAFE $top_hdi 100

  # HDI bond pads, top and bottom
  @HDI_TOP_OUTER CHOICEPOPUP $choice "Ready to survey the middle of the upper bond pad on the HDI (outer edge)?"
  GOTOIFN @HDI_TOP_OUTER $choice
  CALL @SURVEYCORNERS  $top_hdi -> $top_hdi_outer
  COPY $top_hdi_inner $top_hdi_outer
  MUL $len_bondHDI_signed $len_bondpad_HDI $sign
  ADD $top_hdi_inner.x $top_hdi_outer.x $len_bondHDI_signed
  @HDI_TOP_INNER CHOICEPOPUP $choice "Ready to survey the middle of the upper bond pad on the HDI (inner edge)?"
  GOTOIFN @HDI_TOP_INNER $choice
  CALL @SURVEYCORNERS $top_hdi_inner -> $top_hdi_inner
  ADD $top_hdi $top_hdi_outer $top_hdi_inner
  MUL $top_hdi $top_hdi {0.5,0.5,0.5}
  COPY $bot_hdi.z $top_hdi.z

  @HDI_BOT_OUTER CHOICEPOPUP $choice "Ready to survey the middle of the lower bond pad on the HDI (outer edge)?"
  GOTOIFN @HDI_BOT_OUTER $choice
  CALL @SURVEYCORNERS  $bot_hdi -> $bot_hdi_outer
  COPY $bot_hdi_inner $bot_hdi_outer
  ADD $bot_hdi_inner.x $bot_hdi_outer.x $len_bondHDI_signed
  @HDI_BOT_INNER CHOICEPOPUP $choice "Ready to survey the middle of the lower bond pad on the HDI (inner edge)?"
  GOTOIFN @HDI_BOT_INNER $choice
  CALL @SURVEYCORNERS $bot_hdi_inner -> $bot_hdi_inner
  ADD $bot_hdi $bot_hdi_outer $bot_hdi_inner
  MUL $bot_hdi $bot_hdi {0.5,0.5,0.5}

  @ROC_TOP_1 CHOICEPOPUP $choice "Ready to survey one corner of the ROC's uppermost bond pad?"
  GOTOIFN @ROC_TOP_1 $choice
  CALL @SURVEYCORNERS $top_ROC -> $top_ROC_1
  @ROC_TOP_2 CHOICEPOPUP $choice "Ready to survey the opposite corner of the ROC's uppermost bond pad?"
  GOTOIFN @ROC_TOP_2 $choice
  CALL @SURVEYCORNERS $top_ROC_1 -> $top_ROC_2
  ADD $top_ROC $top_ROC_1 $top_ROC_2
  MUL $top_ROC $top_ROC {0.5,0.5,0.5}
  COPY $bot_ROC.z $top_ROC.z

  @ROC_BOT_1 CHOICEPOPUP $choice "Ready to survey one corner of the ROC's lowest bond pad?"
  GOTOIFN @ROC_BOT_1 $choice
  CALL @SURVEYCORNERS $bot_ROC -> $bot_ROC_1
  @ROC_BOT_2 CHOICEPOPUP $choice "Ready to survey the opposite corner of the ROC's lowest bond pad?"
  GOTOIFN @ROC_BOT_2 $choice
  CALL @SURVEYCORNERS $bot_ROC_1 -> $bot_ROC_2
  ADD $bot_ROC $bot_ROC_1 $bot_ROC_2
  MUL $bot_ROC $bot_ROC {0.5,0.5,0.5}

  CALL @SURVEYCALC $top_hdi $bot_hdi $top_ROC -> $d_upper_x $d_upper_y
  CALL @SURVEYCALC $bot_hdi $top_hdi $bot_ROC -> $d_lower_x $d_lower_y

  SUB $d_DeltaX $d_upper_x $d_lower_x
  ADD $avg_DeltaY $d_upper_y $d_lower_y
  MUL $avg_DeltaY $avg_DeltaY 0.5
  RETURN $d_DeltaX $avg_DeltaY


# SURVEYCALC
# Arguments:
#  $near_hdi - Center position for the nearest HDI bond pad
#  $far_hdi - Center position for the furthest HDI bond pad
#  $ROC - Center position for the current ROC bond pad
#
# Returns:
#  $delta_x
#  $delta_y
@SURVEYCALC(near_hdi,far_hdi,ROC)
  #Variables
  COPY $a $near_hdi.x
  COPY $b $near_hdi.y
  COPY $c $far_hdi.x
  COPY $d $far_hdi.y
  COPY $e $ROC.x
  COPY $f $ROC.y

  #Slope
  SUB $b_min_d $b $d
  SUB $a_min_c $a $c
  INVERT $a_min_c $a_min_c
  MUL $slope $b_min_d $a_min_c

  #Calulating numerator for x
  MUL $slope_2 $slope $slope
  MUL $top_one $a $slope_2
  SUB $f_min_b $f $b
  MUL $top_two $f_min_b $slope
  ADD $top_oneTwo $top_one $top_two
  ADD $top $top_oneTwo $e

  #Calculating denominator for x
  ADD $bottom $slope_2 1
  INVERT $bottom $bottom

  #Calculating x and y
  MUL $x $top $bottom
  INVERT $inv_slope $slope
  MUL $inv_slope $inv_slope -1
  SUB $x_min_e $x $e
  MUL $y_one $inv_slope $x_min_e
  ADD $y $y_one $f

  #Calculating deltas
  SUB $a_min_x $a $x
  SUB $b_min_y $b $y
  SUB $e_min_x $e $x
  SUB $f_min_y $f $y

  POW $a_min_x_2 $a_min_x 2
  POW $b_min_y_2 $b_min_y 2
  ADD $delta_y_2 $a_min_x_2 $b_min_y_2
  POW $delta_y $delta_y_2 0.5

  ABS $abs_b_min_y $b_min_y
  INVERT $abs_b_min_y $abs_b_min_y
  MUL $sign_b_min_y $b_min_y $abs_b_min_y
  MUL $delta_y $sign_b_min_y $delta_y

  PRINT "Delta y: %f" $delta_y

  POW $e_min_x_2 $e_min_x 2
  POW $f_min_y_2 $f_min_y 2
  ADD $delta_x_2 $e_min_x_2 $f_min_y_2
  POW $delta_x $delta_x_2 0.5
  RETURN $delta_x $delta_y
