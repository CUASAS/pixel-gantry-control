VERSION 1.3

######################## RD53A Single Sensor Assembly #############################
# Steps:
# 1. Prompt placement of HDI on Chuck 2
# 2. Turn on Chuck 2's vacuum
# 3. Move gantry head to Chuck 2 and survey 4 corners of HDI (defined by bond pads)
# 4. Compute would-be position and rotation of HDI on Chuck 1
# 5. Prompt placement of SensorROC on the launchpad
#    i. Is Chuck 1 clear?
#    ii. Is the Pickup Tool ready?
# 6. Survey the SensorROC on the launchpad
# 7. Pick up the SensorROC and place on Chuck 1 at position computed in Step 4
# 8. Prompt placement of vacuum tool on HDI on Chuck 2
# 9. Transfer vacuum from Chuck 2 to Vacuum Tool (Chuck 3)
# 10. Check that HDI was moved to Chuck 3 and the adhesive was applied
# 11. Prompt placement of Vacuum Tool bearing HDI on Chuck 1

#Initialize the system
HOME
SETVAC gantry_head_inner 0
SETVAC gantry_head_outer 0
SETVAC rd53a_chuck_1_TL 0
SETVAC rd53a_chuck_1_TR 0
SETVAC rd53a_chuck_1_BL 0
SETVAC rd53a_chuck_1_BR 0
SETVAC rd53a_chuck_2 0
SETVAC rd53a_chuck_3 0
SETVAC launchpad 0
SETLOG "LOG_Assembly_RD53A_SingleSensor_Start.txt"
SETERRORMODE prompt

# Rough positions of the HDI on Chuck 2
COPY $pos_chuck2_tr {460.391,528.560,100.447}
COPY $pos_chuck2_br {460.430,568.240,100.523}
COPY $pos_chuck2_bl {440.753,568.246,100.478}
COPY $pos_chuck2_tl {440.705,528.560,100.523}

# Rough positions of the SensorROC on Launchpad
COPY $pos_senRoc_launch_tr {572.531,199.625,87.952}
COPY $pos_senRoc_launch_br {572.449,240.370,87.952}
COPY $pos_senRoc_launch_bl {549.212,240.418,87.952}
COPY $pos_senRoc_launch_tl {549.226,199.595,87.952}

# HDI pick and place translation vector and rotation angle measured independently
COPY $v_hdi_translation {-49.833,0.065,-6.35}
COPY $rot_hdi_translation -0.026

# SensorROC translation correctional terms measured independently
COPY $v_senRoc_error {-0.003,0.007,0}
COPY $rot_senRoc_error -0.007

# Step 1. Prompt placement of HDI on Chuck 2
PRINT "--------------------"
@STEP_ONE CHOICEPOPUP $choice "Have you placed the RD53A HDI on Chuck 2?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_ONE $choice

# Step 2. Turn on Chuck 2's vacuum

SETVAC rd53a_chuck_2 1

# Step 3. Move gantry head to Chuck 2 and survey 4 corners of HDI (defined by bond pads)

MOVESAFE $pos_chuck2_tr 100
COPY $x_corner $pos_chuck2_tr
@HDI_0 CHOICEPOPUP $choice "Ready to find the Top Right marker?" "Yes" "No"
GOTOIFN @HDI_0 $choice
CALL @SURVEYCORNERS
COPY $pos_chuck2_tr $x_corner

COPY $x_corner $pos_chuck2_br
@HDI_1 CHOICEPOPUP $choice "Ready to find the Bottom Right marker?" "Yes" "No"
GOTOIFN @HDI_1 $choice
CALL @SURVEYCORNERS
COPY $pos_chuck2_br $x_corner

COPY $x_corner $pos_chuck2_bl
@HDI_2 CHOICEPOPUP $choice "Ready to find the Bottom Left marker?" "Yes" "No"
GOTOIFN @HDI_2 $choice
CALL @SURVEYCORNERS
COPY $pos_chuck2_bl $x_corner

COPY $x_corner $pos_chuck2_tl
@HDI_3 CHOICEPOPUP $choice "Ready to find the Top Left marker?" "Yes" "No"
GOTOIFN @HDI_3 $choice
CALL @SURVEYCORNERS
COPY $pos_chuck2_tl $x_corner

# Step 4. Compute would-be position and rotation of the HDI on Chuck 1

FIT $pos_hdi_center $rot_hdi TFPX_RD53AHDI $pos_chuck2_tr $pos_chuck2_br $pos_chuck2_bl $pos_chuck2_tl

ADD $pos_hdi_center $pos_hdi_center $v_hdi_translation

QUAT2EULER $rot_hdi $trash0 $trash1 $rot_hdi
ADD $rot_hdi $rot_hdi $rot_hdi_translation

MOVESAFE {350,150,0} 100

# Step 5. Prompt placement of SensorROC
@STEP_FIVE CHOICEPOPUP $choice "Have you placed the SensorROC on the launchpad?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_FIVE $choice
SETVAC launchpad 1
@STEP_FIVE_i CHOICEPOPUP $choice "Is Chuck 1 clear?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_FIVE_i $choice
@STEP_FIVE_ii CHOICEPOPUP $choice "Is pickup tool ready?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_FIVE_ii $choice

# Step 6. Survey the SensorROC

MOVESAFE $pos_senRoc_launch_tr 100
COPY $x_corner $pos_senRoc_launch_tr
@ROC_0 CHOICEPOPUP $choice "Ready to find the Top Right marker?" "Yes" "No"
GOTOIFN @ROC_0 $choice
CALL @SURVEYCORNERS
COPY $pos_senRoc_launch_tr $x_corner

COPY $x_corner $pos_senRoc_launch_br
@ROC_1 CHOICEPOPUP $choice "Ready to find the Bottom Right marker?" "Yes" "No"
GOTOIFN @ROC_1 $choice
CALL @SURVEYCORNERS
COPY $pos_senRoc_launch_br $x_corner

COPY $x_corner $pos_senRoc_launch_bl
@ROC_2 CHOICEPOPUP $choice "Ready to find the Bottom Left marker?" "Yes" "No"
GOTOIFN @ROC_2 $choice
CALL @SURVEYCORNERS
COPY $pos_senRoc_launch_bl $x_corner

COPY $x_corner $pos_senRoc_launch_tl
@ROC_3 CHOICEPOPUP $choice "Ready to find the Top Left marker?" "Yes" "No"
GOTOIFN @ROC_3 $choice
CALL @SURVEYCORNERS
COPY $pos_senRoc_launch_tl $x_corner

# Calculate the orientation of the SensorROC
FIT $pos_senRoc_center $rot_senRoc TFPX_RD53ASENSORROC $pos_senRoc_launch_tr $pos_senRoc_launch_br $pos_senRoc_launch_bl $pos_senRoc_launch_tl

QUAT2EULER $rot_senRoc $trash0 $trash1 $rot_senRoc

PRINT "Sen Roc"
PRINT "Rot: %f Center: %v" $rot_senRoc $pos_senRoc_center
PRINT "HDI"
PRINT "Rot: %f Center: %v" $rot_hdi $pos_hdi_center
PRINT ""

CHOICEPOPUP $choice "Are angles and positions reasonable (See console)?" "Yes, continue" "No"
GOTOIF @ANGLE_CHECK $choice
END
@ANGLE_CHECK

# Step 7. Pick up the SensorROC and place on Chuck 1 at position computed in Step 4

ADD $pos_hdi_center_withError $pos_hdi_center $v_senRoc_error
ADD $rot_hdi_withError $rot_hdi $rot_senRoc_error

LOADTOOL picker_tool
MOVENAME stagingArea

# Pickup Step
COPY $target_pos $pos_senRoc_center
CALL @CALCPICKUP
MOVETO $pickup_pos_z0 50
ROTATE $rot_senRoc
SUB $pickup_pos.z $pickup_pos.z 5
MOVETO $pickup_pos 10
ADD $pickup_pos.z $pickup_pos.z 5.5
MOVETO $pickup_pos 1
WAIT 500
SETVAC gantry_head_inner 1
WAIT 1000
SETVAC launchpad 0
WAIT 2000
MOVETO $pickup_pos_z0 10
MUL $neg_rot_senRoc $rot_senRoc -1.0
ROTATE $neg_rot_senRoc

# Place Step
COPY $target_pos $pos_hdi_center_withError
CALL @CALCPICKUP
MOVETO $pickup_pos_z0 50
ROTATE $rot_hdi_withError
SUB $pickup_pos.z $pickup_pos.z 5
MOVETO $pickup_pos 10
ADD $pickup_pos.z $pickup_pos.z 5
MOVETO $pickup_pos 1
WAIT 500
SETVAC rd53a_chuck_1_TR 1
SETVAC rd53a_chuck_1_BL 1
SETVAC rd53a_chuck_1_TL 1
SETVAC rd53a_chuck_1_BR 1
WAIT 1000
SETVAC gantry_head_inner 0
WAIT 2000
MOVETO $pickup_pos_z0 10
MUL $neg_rot_hdi_withError $rot_hdi_withError -1.0
ROTATE $neg_rot_hdi_withError

UNLOADTOOL

MOVESAFE {350,150,0} 100

# Step 8. Prompt placement of vacuum tool on HDI on Chuck 2
@STEP_EIGHT CHOICEPOPUP $choice "Have you placed the Vacuum Tool on the HDI in Chuck 2 such that the fiducials are visible through its viewing holes?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_EIGHT $choice

# Step 9. Transfer vacuum from Chuck 2 to Vacuum Tool (Chuck 3)

SETVAC rd53a_chuck_3 1
WAIT 1000
SETVAC rd53a_chuck_2 0

# Step 10. Check that HDI was moved to Chuck 3 and the adhesive was applied
@STEP_TEN CHOICEPOPUP $choice "Did you move the HDI to Chuck 3 and apply the adhesive?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_TEN $choice

# Step 11. Prompt placement of Vacuum Tool bearing HDI on Chuck 1
@STEP_ELEVEN CHOICEPOPUP $choice "Did you place the HDI on the SensorROC on Chuck 1 (With adhesive between them)?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_ELEVEN $choice

PRINT "--------------------"

HOME

END

# SURVEYCORNERS
# Arguments
# $x_corner - Approximate position of an object that you would like to know the precise position. Returns with precise position written in this location
@SURVEYCORNERS PASS
  MOVETO $x_corner 10
  @SURCORNPOP VIDEO
  CHOICEPOPUP $choice "Did you correctly align the cursor with the marker?" "Yes" "No"
  GOTOIFN @SURCORNPOP $choice
  GETPOST $x_corner
  GOTO $RET

# CALCPICKUP
# Arguments:
#  $target_pos - center of piece to pickup/place
@CALCPICKUP FLEXREAD $picker_height geometry.PICKER_TOOL.center_offset
  FLEXREAD $ghco geometry.tool_holder_offset
  ADD $netoffset $picker_height $ghco
  SUB $pickup_pos $target_pos $netoffset
  COPY $pickup_pos_z0 $pickup_pos
  COPY $pickup_pos_z0.z 0
  GOTO $RET
