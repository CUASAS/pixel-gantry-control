# This script is to configure the gantry's tool_holder_offset (ghco) vector in the flex_config

LOADCONFIG
HOME
SETLOG "LOG_Calibrate_GHCO.txt"

SETVAC gantry_head_inner 0

COPY $launch_tr {652.280908,194.164640,88.614000}
COPY $launch_br {652.099515,216.113655,88.639000}
COPY $launch_bl {636.143686,215.977490,88.614000}
COPY $launch_tl {636.325542,194.027209,88.614000}


COPY $targetPos {571.669621,215.414145,87.984001}

FLEXREAD $dim geometry.TFPX_GHCOPIECE.fid_br

COPY $angle 0





@STEP_ZERO PASS
MOVENAME stagingArea 100

MOVESAFE $launch_tr 50
CALL @SURVEYCORNERS $launch_tr -> $launch_tr
MOVESAFE $launch_br 50
CALL @SURVEYCORNERS $launch_br -> $launch_br
MOVESAFE $launch_bl 50
CALL @SURVEYCORNERS $launch_bl -> $launch_bl
MOVESAFE $launch_tl 50
CALL @SURVEYCORNERS $launch_tl -> $launch_tl

FIT $pos $pickupRot TFPX_GHCOPIECE $launch_tr $launch_br $launch_bl $launch_tl
QUAT2EULER $pickupRot $trash0 $trash1 $pickupRot

LOADTOOL picker_tool

CALL @CALCPICKUP $pos -> $pickupPos $pickupPos_z0
CALL @PICKUPSTEP $pickupPos $pickupPos_z0 $pickupRot

CALL @CALCPICKUP $targetPos -> $placePos $placePos_z0
CALL @PLACESTEP $placePos $placePos_z0 $angle

ROTATETO 0 20
UNLOADTOOL

CALL @CALCCORNERS $targetPos $angle $dim -> $final_tr $final_br $final_bl $final_tl

MOVESAFE $final_tr 50
CALL @SURVEYCORNERS $final_tr -> $final_tr
MOVESAFE $final_br 50
CALL @SURVEYCORNERS $final_br -> $final_br
MOVESAFE $final_bl 50
CALL @SURVEYCORNERS $final_bl -> $final_bl
MOVESAFE $final_tl 50
CALL @SURVEYCORNERS $final_tl -> $final_tl

FIT $pos_place $rot_place TFPX_GHCOPIECE $final_tr $final_br $final_bl $final_tl
QUAT2EULER $rot_place $trash0 $trash1 $rot_place

PRINT "------------------"
PRINT "       Angle:   %f" $angle
PRINT "   Top Right:   %v" $final_tr
PRINT "Bottom Right:   %v" $final_br
PRINT " Bottom Left:   %v" $final_bl
PRINT "    Top Left:   %v" $final_tl
PRINT "Fit position:   %v" $pos_place
PRINT "   Fit angle:   %f" $rot_place
PRINT "------------------"

SUB $test_cond $angle 90
ADD $angle $angle 10

GOTOIF @STEP_ZERO $test_cond

END


# SURVEYCORNERS
# Arguments:
# $x_corner - Approximate position of an object that you would like to know the precise position. Returns with precise position written in this location
#
# Returns:
# $x_corner - Updated position of the corner that is being looked for
@SURVEYCORNERS(x_corner)
  MOVESAFE $x_corner 100
  @SURCORNPOP VIDEO
  CHOICEPOPUP $choice "Did you correctly align the cursor with the marker?" "Yes" "No"
  GOTOIFN @SURCORNPOP $choice
  GETPOS $x_corner
  RETURN $x_corner


# CALCPICKUP
# Arguments:
#  $target_pos - center of piece to pickup/place
#  $target_rot - rotation of piece to pickup/place
#
# Returns:
#  $pickup_pos - pickup position
#  $pickup_pos_z0 - pickup position with z = 0
@CALCPICKUP(target_pos)
  FLEXREAD $picker_height geometry.PICKER_TOOL.center_offset #geometry.PICKER_TOOL.center_offset
  FLEXREAD $ghco geometry.tool_holder_offset
  ADD $netoffset $picker_height $ghco
  SUB $pickup_pos $target_pos $netoffset
  COPY $pickup_pos_z0 $pickup_pos
  COPY $pickup_pos_z0.z 0
  RETURN $pickup_pos $pickup_pos_z0


# PICKUPSTEP
# takes place directly after CALCPICKUP_4ROC
# Arguments:
#  $pick_pos - pickup position
#  $pick_pos_z0 - pickup position with z = 0
#  $pick_rot - rotation of the what is being picked up
#
# No Returns
@PICKUPSTEP(pick_pos,pick_pos_z0,pick_rot)
  MOVETO $pick_pos_z0 50
  ROTATETO $pick_rot 10
  SUB $pick_pos.z $pick_pos.z 2
  MOVETO $pick_pos 50
  ADD $pick_pos.z $pick_pos.z 2
  MOVETO $pick_pos 1
  WAIT 500
  SETVAC gantry_head_inner 1
  WAIT 2000
  #SETVAC launchpad 0
  WAIT 1000
  SUB $pick_pos.z $pick_pos.z 2
  MOVETO $pick_pos 1
  MOVETO $pick_pos_z0 50
  RETURN


# PLACESTEP
# takes place directly after CALCPICKUP_4ROC
# Arguments:
#  $place_pos - pickup position
#  $place_pos_z0 - pickup position with z = 0
#  $place_rot - rotation of the what is being picked up
#
# No Returns
@PLACESTEP(place_pos,place_pos_z0,place_rot)
  MOVETO $place_pos_z0 50
  ROTATETO $place_rot 10
  SUB $place_pos.z $place_pos.z 2
  MOVETO $place_pos 50
  ADD $place_pos.z $place_pos.z 2
  MOVETO $place_pos 1
  WAIT 1000
  SETVAC gantry_head_inner 0
  WAIT 2000
  SUB $place_pos.z $place_pos.z 2
  MOVETO $place_pos 1
  MOVETO $place_pos_z0 50
  RETURN


# CALCCORNERS
# Arguments:
#  $center - center of object
#  $rotation - rotation of the object
#  $dimensions - dimensions of object (as a vector)
#
# Returns
#  $topRight - position of the top right corner
#  $botRight - position of the bottom right corner
#  $botLeft - position of the bottom left corner
#  $topLeft - position of the top left corner
@CALCCORNERS(center,rotation,dimensions)
  SIN $sin_angle $rotation
  COS $cos_angle $rotation
  COPY $width $dimensions.x
  COPY $length $dimensions.y
  MUL $w_cos $width $cos_angle
  MUL $w_sin $width $sin_angle
  MUL $l_cos $length $cos_angle
  MUL $l_sin $length $sin_angle

  ADD $d_x_TR $l_sin $w_cos
  ADD $topRight $center.x $d_x_TR
  SUB $d_y_TR $w_sin $l_cos
  ADD $topRight.y $center.y $d_y_TR
  COPY $topRight.z $center.z

  SUB $d_x_BR $w_cos $l_sin
  ADD $botRight $center.x $d_x_BR
  ADD $d_y_BR $w_sin $l_cos
  ADD $botRight.y $center.y $d_y_BR
  COPY $botRight.z $center.z

  ADD $d_x_BL $w_cos $l_sin
  SUB $botLeft $center.x $d_x_BL
  SUB $d_y_BL $l_cos $w_sin
  ADD $botLeft.y $center.y $d_y_BL
  COPY $botLeft.z $center.z

  SUB $d_x_TL $l_sin $w_cos
  ADD $topLeft $center.x $d_x_TL
  ADD $d_y_TL $l_cos $w_sin
  SUB $topLeft.y $center.y $d_y_TL
  COPY $topLeft.z $center.z

  RETURN $topRight $botRight $botLeft $topLeft
