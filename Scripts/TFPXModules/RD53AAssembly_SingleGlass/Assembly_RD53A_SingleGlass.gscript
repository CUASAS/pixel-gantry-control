VERSION v2.1

######################## RD53A Single Glass Assembly #############################
# Steps:
# 1. Prompt placement of HDI on Chuck 2
# 2. Turn on Chuck 2's vacuum
# 3. Move gantry head to Chuck 2 and survey 4 corners of HDI (defined by bond pads)
# 4. Compute would-be position and rotation of HDI on Chuck 1
# 5. Prompt placement of MockROC is on the launchpad
#    i. Is Chuck 1 clear?
#    ii. Is the Pickup Tool ready?
# 6. Survey the MockROC on the launchpad
# 7. Pick up the MockROC and place on Chuck 1 at position computed in Step 4
# 8. Prompt placement of vacuum tool on HDI on Chuck 2
# 9. Transfer vacuum from Chuck 2 to Vacuum Tool (Chuck 3)
# 10. Check that HDI was moved to Chuck 3 and the adhesive was applied
# 11. Prompt placement of Vacuum Tool bearing HDI on Chuck 1
# 12. Release vacuum of Vacuum Tool
# 13. Prompt replacement of Vacuum Tool on Chuck 3
# 14. Calculate the placement of the HDI and glass piece
# 15. Move gantry head to Chuck 1 and survey for Module accuracy
# 16. End program with vacuums on or off

#Initialize the system
LOADCONFIG
HOME
SETVAC gantry_head_inner 0
SETVAC gantry_head_outer 0
SETVAC rd53a_chuck_1_TL 0
SETVAC rd53a_chuck_1_TR 0
SETVAC rd53a_chuck_1_BL 0
SETVAC rd53a_chuck_1_BR 0
SETVAC rd53a_chuck_2 0
SETVAC rd53a_chuck_3 0
SETVAC launchpad 0
SETLOG "LOG_Assembly_RD53A_SingleGlass_DataGeneration.txt"
SETERRORMODE prompt

# Rough positions of the HDI on Chuck 2
FLEXREAD $pos_chuck2_tr siteGeometry.pos_chuck2_tr
FLEXREAD $pos_chuck2_br siteGeometry.pos_chuck2_br
FLEXREAD $pos_chuck2_bl siteGeometry.pos_chuck2_bl
FLEXREAD $pos_chuck2_tl siteGeometry.pos_chuck2_tl

# Rough positions of the MockROC on Launchpad
FLEXREAD $pos_senRoc_launch_tr siteGeometry.pos_senRoc_launch_tr
FLEXREAD $pos_senRoc_launch_br siteGeometry.pos_senRoc_launch_br
FLEXREAD $pos_senRoc_launch_bl siteGeometry.pos_senRoc_launch_bl
FLEXREAD $pos_senRoc_launch_tl siteGeometry.pos_senRoc_launch_tl

# Dimensions of the HDI and MockROC (bond pads or corners)
FLEXREAD $dim_HDI geometry.TFPX_RD53AHDI.fid_br
FLEXREAD $dim_senRoc geometry.TFPX_SINGLEGLASS.fid_br
COPY $dim_HDI.z 0.241     # The change in z from senRoc -> HDI

# HDI pick and place translation vector and rotation angle measured independently
FLEXREAD $v_hdi_translation siteGeometry.v_hdi_translation
FLEXREAD $rot_hdi_translation siteGeometry.rot_hdi_translation

# MockROC translation correctional terms measured independently
FLEXREAD $v_senRoc_error siteGeometry.v_senRoc_error
FLEXREAD $rot_senRoc_error siteGeometry.rot_senRoc_error

# Step 1. Prompt placement of HDI on Chuck 2
PRINT "--------------------"
@STEP_ONE CHOICEPOPUP $choice "Have you placed the RD53A HDI on Chuck 2?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_ONE $choice

# Step 2. Turn on Chuck 2's vacuum

SETVAC rd53a_chuck_2 1

# Step 3. Move gantry head to Chuck 2 and survey 4 corners of HDI (defined by bond pads)

MOVESAFE $pos_chuck2_tr 100
@HDI_0 CHOICEPOPUP $choice "Ready to find the Top Right marker?" "Yes" "No"
GOTOIFN @HDI_0 $choice
CALL @SURVEYCORNERS $pos_chuck2_tr -> $pos_chuck2_tr

@HDI_1 CHOICEPOPUP $choice "Ready to find the Bottom Right marker?" "Yes" "No"
GOTOIFN @HDI_1 $choice
CALL @SURVEYCORNERS $pos_chuck2_br -> $pos_chuck2_br

@HDI_2 CHOICEPOPUP $choice "Ready to find the Bottom Left marker?" "Yes" "No"
GOTOIFN @HDI_2 $choice
CALL @SURVEYCORNERS $pos_chuck2_bl -> $pos_chuck2_bl

@HDI_3 CHOICEPOPUP $choice "Ready to find the Top Left marker?" "Yes" "No"
GOTOIFN @HDI_3 $choice
CALL @SURVEYCORNERS $pos_chuck2_tl -> $pos_chuck2_tl

# Step 4. Compute would-be position and rotation of the HDI on Chuck 1

FIT $pos_hdi_center $rot_hdi TFPX_RD53AHDI $pos_chuck2_tr $pos_chuck2_br $pos_chuck2_bl $pos_chuck2_tl

ADD $pos_hdi_center $pos_hdi_center $v_hdi_translation

QUAT2EULER $rot_hdi $trash0 $trash1 $rot_hdi
ADD $rot_hdi $rot_hdi $rot_hdi_translation

MOVESAFE {350,150,0} 100

# Step 5. Prompt placement of MockROC
@STEP_FIVE CHOICEPOPUP $choice "Have you placed the MockROC on the launchpad?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_FIVE $choice
SETVAC launchpad 1
@STEP_FIVE_i CHOICEPOPUP $choice "Is Chuck 1 clear?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_FIVE_i $choice
@STEP_FIVE_ii CHOICEPOPUP $choice "Is pickup tool ready?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_FIVE_ii $choice

# Step 6. Survey the MockROC

MOVESAFE $pos_senRoc_launch_tr 100
@ROC_0 CHOICEPOPUP $choice "Ready to find the Top Right marker?" "Yes" "No"
GOTOIFN @ROC_0 $choice
CALL @SURVEYCORNERS $pos_senRoc_launch_tr -> $pos_senRoc_launch_tr

@ROC_1 CHOICEPOPUP $choice "Ready to find the Bottom Right marker?" "Yes" "No"
GOTOIFN @ROC_1 $choice
CALL @SURVEYCORNERS $pos_senRoc_launch_br -> $pos_senRoc_launch_br

@ROC_2 CHOICEPOPUP $choice "Ready to find the Bottom Left marker?" "Yes" "No"
GOTOIFN @ROC_2 $choice
CALL @SURVEYCORNERS $pos_senRoc_launch_bl -> $pos_senRoc_launch_bl

@ROC_3 CHOICEPOPUP $choice "Ready to find the Top Left marker?" "Yes" "No"
GOTOIFN @ROC_3 $choice
CALL @SURVEYCORNERS $pos_senRoc_launch_tl -> $pos_senRoc_launch_tl

# Calculate the orientation of the MockROC
FIT $pos_senRoc_center $rot_senRoc TFPX_SINGLEGLASS $pos_senRoc_launch_tr $pos_senRoc_launch_br $pos_senRoc_launch_bl $pos_senRoc_launch_tl

QUAT2EULER $rot_senRoc $trash0 $trash1 $rot_senRoc

PRINT "Sen Roc"
PRINT "Rot: %f Center: %v" $rot_senRoc $pos_senRoc_center
PRINT "HDI"
PRINT "Rot: %f Center: %v" $rot_hdi $pos_hdi_center
PRINT ""

CHOICEPOPUP $choice "Are angles and positions reasonable (See console)?" "Yes, continue" "No"
GOTOIF @ANGLE_CHECK $choice
END
@ANGLE_CHECK

# Step 7. Pick up the MockROC and place on Chuck 1 at position computed in Step 4

ADD $pos_hdi_center_withError $pos_hdi_center $v_senRoc_error
ADD $rot_hdi_withError $rot_hdi $rot_senRoc_error

MOVENAME stagingArea
LOADTOOL picker_tool
MOVENAME stagingArea

# Pickup Step
CALL @CALCPICKUP $pos_senRoc_center -> $pickup_pos $pickup_pos_z0
MOVETO $pickup_pos_z0 50
ROTATE $rot_senRoc
SUB $pickup_pos.z $pickup_pos.z 5
MOVETO $pickup_pos 10
ADD $pickup_pos.z $pickup_pos.z 5
MOVETO $pickup_pos 1
WAIT 500
SETVAC gantry_head_inner 1
WAIT 1000
SETVAC launchpad 0
WAIT 2000
MOVETO $pickup_pos_z0 10
MUL $neg_rot_senRoc $rot_senRoc -1.0
ROTATE $neg_rot_senRoc

# Place Step
CALL @CALCPICKUP $pos_hdi_center_withError -> $pickup_pos $pickup_pos_z0
MOVETO $pickup_pos_z0 50
ROTATE $rot_hdi_withError
SUB $pickup_pos.z $pickup_pos.z 5
MOVETO $pickup_pos 10
ADD $pickup_pos.z $pickup_pos.z 5
MOVETO $pickup_pos 1
WAIT 500
SETVAC rd53a_chuck_1_TR 1
SETVAC rd53a_chuck_1_BL 1
SETVAC rd53a_chuck_1_TL 1
SETVAC rd53a_chuck_1_BR 1
WAIT 1000
SETVAC gantry_head_inner 0
WAIT 2000
MOVETO $pickup_pos_z0 10
MUL $neg_rot_hdi_withError $rot_hdi_withError -1.0
ROTATE $neg_rot_hdi_withError

UNLOADTOOL

MOVESAFE {350,150,0} 100

# Step 8. Prompt placement of vacuum tool on HDI on Chuck 2
@STEP_EIGHT CHOICEPOPUP $choice "Have you placed the Vacuum Tool on the HDI in Chuck 2 such that the fiducials are visible through its viewing holes?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_EIGHT $choice

# Step 9. Transfer vacuum from Chuck 2 to Vacuum Tool (Chuck 3)

SETVAC rd53a_chuck_3 1
WAIT 1000
SETVAC rd53a_chuck_2 0

# Step 10. Check that HDI was moved to Chuck 3 and the adhesive was applied
@STEP_TEN CHOICEPOPUP $choice "Did you move the HDI to Chuck 3 and apply the adhesive?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_TEN $choice

# Step 11. Prompt placement of Vacuum Tool bearing HDI on Chuck 1
@STEP_ELEVEN CHOICEPOPUP $choice "Did you place the HDI on the MockROC on Chuck 1 (With adhesive between them)?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_ELEVEN $choice

# Step 12. Release vacuum of Vacuum Tool

SETVAC rd53a_chuck_3 0

# Step 13. Prompt replacement of Vacuum Tool on Chuck 3
@STEP_THIRTEEN CHOICEPOPUP $choice "Did you place the Vacuum Tool on Chuck 3?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_THIRTEEN $choice

# Step 14. Calculate the placement of the HDI and the senRoc

CALL @CALCCORNERS $pos_hdi_center_withError $rot_hdi_withError $dim_senRoc -> $pos_senRoc1_tr $pos_senRoc1_br $pos_senRoc1_bl $pos_senRoc1_tl
SUB $pos_hdi_center_withError.z $pos_hdi_center_withError.z $dim_HDI.z
CALL @CALCCORNERS $pos_hdi_center_withError $rot_hdi_withError $dim_HDI -> $pos_chuck1_tr $pos_chuck1_br $pos_chuck1_bl $pos_chuck1_tl


# Step 15. Move gantry head to Chuck 1 and survey for Module accuracy

MOVESAFE $pos_chuck1_tr 100
@ACC_0 CHOICEPOPUP $choice "Ready to find the Top Right marker of HDI?" "Yes" "No"
GOTOIFN @ACC_0 $choice
CALL @SURVEYCORNERS $pos_chuck1_tr -> $pos_chuck1_tr

@ACC_1 CHOICEPOPUP $choice "Ready to find the Top Right marker of MockROC?" "Yes" "No"
GOTOIFN @ACC_1 $choice
CALL @SURVEYCORNERS $pos_senRoc1_tr -> $pos_senRoc1_tr


@ACC_2 CHOICEPOPUP $choice "Ready to find the Bottom Right marker of HDI?" "Yes" "No"
GOTOIFN @ACC_2 $choice
CALL @SURVEYCORNERS $pos_chuck1_br -> $pos_chuck1_br

@ACC_3 CHOICEPOPUP $choice "Ready to find the Bottom Right marker of MockROC?" "Yes" "No"
GOTOIFN @ACC_3 $choice
CALL @SURVEYCORNERS $pos_senRoc1_br -> $pos_senRoc1_br


@ACC_4 CHOICEPOPUP $choice "Ready to find the Bottom Left marker of HDI?" "Yes" "No"
GOTOIFN @ACC_4 $choice
CALL @SURVEYCORNERS $pos_chuck1_bl -> $pos_chuck1_bl

@ACC_5 CHOICEPOPUP $choice "Ready to find the Bottom Left marker of MockROC?" "Yes" "No"
GOTOIFN @ACC_5 $choice
CALL @SURVEYCORNERS $pos_senRoc1_bl -> $pos_senRoc1_bl


@ACC_6 CHOICEPOPUP $choice "Ready to find the Top Left marker of HDI?" "Yes" "No"
GOTOIFN @ACC_6 $choice
CALL @SURVEYCORNERS $pos_chuck1_tl -> $pos_chuck1_tl

@ACC_7 CHOICEPOPUP $choice "Ready to find the Top Left marker of MockROC?" "Yes" "No"
GOTOIFN @ACC_7 $choice
CALL @SURVEYCORNERS $pos_senRoc1_tl -> $pos_senRoc1_tl

#Calculating delta(delta(x)) and avg(Delta(y))
ADD $pos_HDI_upperPad $pos_chuck1_tr $pos_chuck1_tl
MUL $pos_HDI_upperPad $pos_HDI_upperPad {0.5,0.5,0.5}
ADD $pos_HDI_lowerPad $pos_chuck1_br $pos_chuck1_bl
MUL $pos_HDI_lowerPad $pos_HDI_lowerPad {0.5,0.5,0.5}

ADD $pos_senROC_upperPad $pos_senRoc1_tr $pos_senRoc1_tl
MUL $pos_senROC_upperPad $pos_senROC_upperPad {0.5,0.5,0.5}
ADD $pos_senROC_lowerPad $pos_senRoc1_br $pos_senRoc1_bl
MUL $pos_senROC_lowerPad $pos_senROC_lowerPad {0.5,0.5,0.5}

CALL @SURVEYCALC $pos_HDI_upperPad $pos_HDI_lowerPad $pos_senROC_upperPad -> $delta_upper_x $delta_upper_y
CALL @SURVEYCALC $pos_HDI_lowerPad $pos_HDI_upperPad $pos_senROC_lowerPad -> $delta_lower_x $delta_lower_y
SUB $deltaDeltaX $delta_upper_x $delta_lower_x
ADD $avgDeltaY $delta_upper_y $delta_lower_y
MUL $avgDeltaY $avgDeltaY 0.5

PRINT "Delta(delta(X)): %f" $deltaDeltaX
PRINT "Avg(delta(Y)): %f" $avgDeltaY

# Step 16. End program with vacuums on or off
CHOICEPOPUP $choice "Do you want to leave the vacuums on?" "Yes, Leave them on" "No, Turn them off"
GOTOIF @STEP_FIFTEEN $choice

SETVAC gantry_head_inner 0
SETVAC gantry_head_outer 0
SETVAC rd53a_chuck_1_TL 0
SETVAC rd53a_chuck_1_TR 0
SETVAC rd53a_chuck_1_BL 0
SETVAC rd53a_chuck_1_BR 0
SETVAC rd53a_chuck_2 0
SETVAC rd53a_chuck_3 0
SETVAC launchpad 0

@STEP_FIFTEEN PASS

HOME

END

# SURVEYCORNERS
# Arguments
#  $corner - Approximate position of an object that you would like to know the precise position. Returns with precise position written in this location
#
# Returns
#  $corner - Updated position of the corner that is wanted
@SURVEYCORNERS(corner)
  MOVETO $corner 50
  @SURCORNPOP VIDEO
  CHOICEPOPUP $choice "Did you correctly align the cursor with the marker?" "Yes" "No"
  GOTOIFN @SURCORNPOP $choice
  GETPOS $corner
  RETURN $corner

# CALCCORNERS
# Arguments:
#  $center - center of object
#  $rotation - rotation of the object
#  $dimensions - dimensions of object (as a vector)
#
# Returns
#  $topRight - position of the top right corner
#  $botRight - position of the bottom right corner
#  $botLeft - position of the bottom left corner
#  $topLeft - position of the top left corner
@CALCCORNERS(center,rotation,dimensions)
  SIN $sin_angle $rotation
  COS $cos_angle $rotation
  COPY $width $dimensions.x
  COPY $length $dimensions.y
  MUL $w_cos $width $cos_angle
  MUL $w_sin $width $sin_angle
  MUL $l_cos $length $cos_angle
  MUL $l_sin $length $sin_angle

  ADD $d_x_TR $l_sin $w_cos
  ADD $topRight $center.x $d_x_TR
  SUB $d_y_TR $w_sin $l_cos
  ADD $topRight.y $center.y $d_y_TR
  COPY $topRight.z $center.z

  SUB $d_x_BR $w_cos $l_sin
  ADD $botRight $center.x $d_x_BR
  ADD $d_y_BR $w_sin $l_cos
  ADD $botRight.y $center.y $d_y_BR
  COPY $botRight.z $center.z

  ADD $d_x_BL $w_cos $l_sin
  SUB $botLeft $center.x $d_x_BL
  SUB $d_y_BL $l_cos $w_sin
  ADD $botLeft.y $center.y $d_y_BL
  COPY $botLeft.z $center.z

  SUB $d_x_TL $l_sin $w_cos
  ADD $topLeft $center.x $d_x_TL
  ADD $d_y_TL $l_cos $w_sin
  SUB $topLeft.y $center.y $d_y_TL
  COPY $topLeft.z $center.z

  RETURN $topRight $botRight $botLeft $topLeft

# CALCPICKUP
# Arguments:
#  $target - center of piece to pickup/place
#
# Returns
#  $pick_pos - pickup position with the tool attached
#  $pick_pos_z0 - pickup position with the tool attached at z = 0
@CALCPICKUP(target)
  FLEXREAD $picker_height geometry.PICKER_TOOL.center_offset
  FLEXREAD $ghco geometry.tool_holder_offset
  ADD $netoffset $picker_height $ghco
  SUB $pick_pos $target $netoffset
  COPY $pick_pos_z0 $pick_pos
  COPY $pick_pos_z0.z 0
  RETURN $pick_pos $pick_pos_z0

# SURVEYCALC
# Arguments:
#  $near_hdi - Center position for the nearest HDI bond pad
#  $far_hdi - Center position for the furthest HDI bond pad
#  $not_hdi - Center position for the current glass/sensor bond pad
#
# Returns:
#  $delta_x
#  $delta_y
@SURVEYCALC(near_hdi,far_hdi,not_hdi)
  #Variables
  COPY $a $near_hdi.x
  COPY $b $near_hdi.y
  COPY $c $far_hdi.x
  COPY $d $far_hdi.y
  COPY $e $not_hdi.x
  COPY $f $not_hdi.y

  #Slope
  SUB $b_min_d $b $d
  SUB $a_min_c $a $c
  INVERT $a_min_c $a_min_c
  MUL $slope $b_min_d $a_min_c

  #Calulating numerator for x
  MUL $slope_2 $slope $slope
  MUL $top_one $a $slope_2
  SUB $f_min_b $f $b
  MUL $top_two $f_min_b $slope
  ADD $top_oneTwo $top_one $top_two
  ADD $top $top_oneTwo $e

  #Calculating denomenator for x
  ADD $bottom $slope_2 1
  INVERT $bottom $bottom

  #Calculating x and y
  MUL $x $top $bottom
  INVERT $inv_slope $slope
  MUL $inv_slope $inv_slope -1
  SUB $x_min_e $x $e
  MUL $y_one $inv_slope $x_min_e
  ADD $y $y_one $f

  #Calculating deltas
  SUB $a_min_x $a $x
  SUB $b_min_y $b $y
  SUB $e_min_x $e $x
  SUB $f_min_y $f $y

  POW $a_min_x_2 $a_min_x 2
  POW $b_min_y_2 $b_min_y 2
  ADD $delta_y_2 $a_min_x_2 $b_min_y_2
  POW $delta_y $delta_y_2 0.5

  ABS $abs_b_min_y $b_min_y
  INVERT $abs_b_min_y $abs_b_min_y
  MUL $sign_b_min_y $b_min_y $abs_b_min_y
  MUL $delta_y $sign_b_min_y $delta_y

  PRINT "Delta y: %f" $delta_y

  POW $e_min_x_2 $e_min_x 2
  POW $f_min_y_2 $f_min_y 2
  ADD $delta_x_2 $e_min_x_2 $f_min_y_2
  POW $delta_x $delta_x_2 0.5
  RETURN $delta_x $delta_y
