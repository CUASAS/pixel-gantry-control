VERSION v2.1

######################## RD53A Single Glass Assembly #############################
# Steps:
# 1. Prompt placement of HDI on Chuck 2
# 2. Turn on Chuck 2's vacuum
# 3. Move gantry head to Chuck 2 and survey 4 corners of HDI (defined by bond pads)
# 4. Compute would-be position and rotation of HDI on Chuck 1
# 5. Prompt placement of MockROC is on the launchpad
#    i. Is Chuck 1 clear?
#    ii. Is the Pickup Tool ready?
# 6. Survey the MockROC on the launchpad
# 7. Pick up the MockROC and place on Chuck 1 at position computed in Step 4
# 8. Prompt placement of vacuum tool on HDI on Chuck 2
# 9. Transfer vacuum from Chuck 2 to Vacuum Tool (Chuck 3)
# 10. Check that HDI was moved to Chuck 3 and the adhesive was applied
# 11. Prompt placement of Vacuum Tool bearing HDI on Chuck 1
# 12. Release vacuum of Vacuum Tool
# 13. Prompt replacement of Vacuum Tool on Chuck 3
# 14. Move gantry head to Chuck 1 and survey for Module accuracy
# 15. End program with vacuums on or off

#Initialize the system
LOADCONFIG
HOME
SETVAC gantry_head_inner 0
SETVAC gantry_head_outer 0
SETVAC rd53a_chuck_1_TL 0
SETVAC rd53a_chuck_1_TR 0
SETVAC rd53a_chuck_1_BL 0
SETVAC rd53a_chuck_1_BR 0
SETVAC rd53a_chuck_2 0
SETVAC rd53a_chuck_3 0
SETVAC launchpad 0
SETLOG "LOG_Assembly_RD53A_SingleGlass_DataGeneration.txt"
SETERRORMODE prompt

# Rough positions of the HDI on Chuck 2
FLEXREAD $pos_chuck2_tr siteGeometry.pos_chuck2_tr
FLEXREAD $pos_chuck2_br siteGeometry.pos_chuck2_br
FLEXREAD $pos_chuck2_bl siteGeometry.pos_chuck2_bl
FLEXREAD $pos_chuck2_tl siteGeometry.pos_chuck2_tl

# Rough positions of the MockROC on Launchpad
FLEXREAD $pos_senRoc_launch_tr siteGeometry.pos_senRoc_launch_tr
FLEXREAD $pos_senRoc_launch_br siteGeometry.pos_senRoc_launch_br
FLEXREAD $pos_senRoc_launch_bl siteGeometry.pos_senRoc_launch_bl
FLEXREAD $pos_senRoc_launch_tl siteGeometry.pos_senRoc_launch_tl

# Rough positions of the HDI on Chuck 1
FLEXREAD $pos_chuck1_tr siteGeometry.pos_chuck1_tr
FLEXREAD $pos_chuck1_br siteGeometry.pos_chuck1_br
FLEXREAD $pos_chuck1_bl siteGeometry.pos_chuck1_bl
FLEXREAD $pos_chuck1_tl siteGeometry.pos_chuck1_tl

# Rough positions of the MockROC on Chuck 1
FLEXREAD $pos_senRoc1_tr siteGeometry.pos_senRoc1_tr
FLEXREAD $pos_senRoc1_br siteGeometry.pos_senRoc1_br
FLEXREAD $pos_senRoc1_bl siteGeometry.pos_senRoc1_bl
FLEXREAD $pos_senRoc1_tl siteGeometry.pos_senRoc1_tl

# HDI pick and place translation vector and rotation angle measured independently
FLEXREAD $v_hdi_translation siteGeometry.v_hdi_translation
FLEXREAD $rot_hdi_translation siteGeometry.rot_hdi_translation

# MockROC translation correctional terms measured independently
FLEXREAD $v_senRoc_error siteGeometry.v_senRoc_error
FLEXREAD $rot_senRoc_error siteGeometry.rot_senRoc_error

# Step 1. Prompt placement of HDI on Chuck 2
PRINT "--------------------"
@STEP_ONE CHOICEPOPUP $choice "Have you placed the RD53A HDI on Chuck 2?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_ONE $choice

# Step 2. Turn on Chuck 2's vacuum

SETVAC rd53a_chuck_2 1

# Step 3. Move gantry head to Chuck 2 and survey 4 corners of HDI (defined by bond pads)

MOVESAFE $pos_chuck2_tr 100
@HDI_0 CHOICEPOPUP $choice "Ready to find the Top Right marker?" "Yes" "No"
GOTOIFN @HDI_0 $choice
CALL @SURVEYCORNERS $pos_chuck2_tr -> $pos_chuck2_tr

@HDI_1 CHOICEPOPUP $choice "Ready to find the Bottom Right marker?" "Yes" "No"
GOTOIFN @HDI_1 $choice
CALL @SURVEYCORNERS $pos_chuck2_br -> $pos_chuck2_br

@HDI_2 CHOICEPOPUP $choice "Ready to find the Bottom Left marker?" "Yes" "No"
GOTOIFN @HDI_2 $choice
CALL @SURVEYCORNERS $pos_chuck2_bl -> $pos_chuck2_bl

@HDI_3 CHOICEPOPUP $choice "Ready to find the Top Left marker?" "Yes" "No"
GOTOIFN @HDI_3 $choice
CALL @SURVEYCORNERS $pos_chuck2_tl -> $pos_chuck2_tl

# Step 4. Compute would-be position and rotation of the HDI on Chuck 1

FIT $pos_hdi_center $rot_hdi TFPX_RD53AHDI $pos_chuck2_tr $pos_chuck2_br $pos_chuck2_bl $pos_chuck2_tl

ADD $pos_hdi_center $pos_hdi_center $v_hdi_translation

QUAT2EULER $rot_hdi $trash0 $trash1 $rot_hdi
ADD $rot_hdi $rot_hdi $rot_hdi_translation

MOVESAFE {350,150,0} 100

# Step 5. Prompt placement of MockROC
@STEP_FIVE CHOICEPOPUP $choice "Have you placed the MockROC on the launchpad?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_FIVE $choice
SETVAC launchpad 1
@STEP_FIVE_i CHOICEPOPUP $choice "Is Chuck 1 clear?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_FIVE_i $choice
@STEP_FIVE_ii CHOICEPOPUP $choice "Is pickup tool ready?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_FIVE_ii $choice

# Step 6. Survey the MockROC

MOVESAFE $pos_senRoc_launch_tr 100
@ROC_0 CHOICEPOPUP $choice "Ready to find the Top Right marker?" "Yes" "No"
GOTOIFN @ROC_0 $choice
CALL @SURVEYCORNERS $pos_senRoc_launch_tr -> $pos_senRoc_launch_tr

@ROC_1 CHOICEPOPUP $choice "Ready to find the Bottom Right marker?" "Yes" "No"
GOTOIFN @ROC_1 $choice
CALL @SURVEYCORNERS $pos_senRoc_launch_br -> $pos_senRoc_launch_br

@ROC_2 CHOICEPOPUP $choice "Ready to find the Bottom Left marker?" "Yes" "No"
GOTOIFN @ROC_2 $choice
CALL @SURVEYCORNERS $pos_senRoc_launch_bl -> $pos_senRoc_launch_bl

@ROC_3 CHOICEPOPUP $choice "Ready to find the Top Left marker?" "Yes" "No"
GOTOIFN @ROC_3 $choice
CALL @SURVEYCORNERS $pos_senRoc_launch_tl -> $pos_senRoc_launch_tl

# Calculate the orientation of the MockROC
FIT $pos_senRoc_center $rot_senRoc TFPX_RD53ASENSORROC $pos_senRoc_launch_tr $pos_senRoc_launch_br $pos_senRoc_launch_bl $pos_senRoc_launch_tl

QUAT2EULER $rot_senRoc $trash0 $trash1 $rot_senRoc

PRINT "Sen Roc"
PRINT "Rot: %f Center: %v" $rot_senRoc $pos_senRoc_center
PRINT "HDI"
PRINT "Rot: %f Center: %v" $rot_hdi $pos_hdi_center
PRINT ""

CHOICEPOPUP $choice "Are angles and positions reasonable (See console)?" "Yes, continue" "No"
GOTOIF @ANGLE_CHECK $choice
END
@ANGLE_CHECK

# Step 7. Pick up the MockROC and place on Chuck 1 at position computed in Step 4

ADD $pos_hdi_center_withError $pos_hdi_center $v_senRoc_error
ADD $rot_hdi_withError $rot_hdi $rot_senRoc_error

LOADTOOL picker_tool
MOVENAME stagingArea

# Pickup Ste
CALL @CALCPICKUP $pos_senRoc_center -> $pickup_pos $pickup_pos_z0
MOVETO $pickup_pos_z0 50
ROTATE $rot_senRoc
SUB $pickup_pos.z $pickup_pos.z 5
MOVETO $pickup_pos 10
ADD $pickup_pos.z $pickup_pos.z 5
MOVETO $pickup_pos 1
WAIT 500
SETVAC gantry_head_inner 1
WAIT 1000
SETVAC launchpad 0
WAIT 2000
MOVETO $pickup_pos_z0 10
MUL $neg_rot_senRoc $rot_senRoc -1.0
ROTATE $neg_rot_senRoc

# Place Step
CALL @CALCPICKUP $pos_hdi_center_withError -> $pickup_pos $pickup_pos_z0
MOVETO $pickup_pos_z0 50
ROTATE $rot_hdi_withError
SUB $pickup_pos.z $pickup_pos.z 5
MOVETO $pickup_pos 10
ADD $pickup_pos.z $pickup_pos.z 5
MOVETO $pickup_pos 1
WAIT 500
SETVAC rd53a_chuck_1_TR 1
SETVAC rd53a_chuck_1_BL 1
SETVAC rd53a_chuck_1_TL 1
SETVAC rd53a_chuck_1_BR 1
WAIT 1000
SETVAC gantry_head_inner 0
WAIT 2000
MOVETO $pickup_pos_z0 10
MUL $neg_rot_hdi_withError $rot_hdi_withError -1.0
ROTATE $neg_rot_hdi_withError

UNLOADTOOL

MOVESAFE {350,150,0} 100

# Step 8. Prompt placement of vacuum tool on HDI on Chuck 2
@STEP_EIGHT CHOICEPOPUP $choice "Have you placed the Vacuum Tool on the HDI in Chuck 2 such that the fiducials are visible through its viewing holes?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_EIGHT $choice

# Step 9. Transfer vacuum from Chuck 2 to Vacuum Tool (Chuck 3)

SETVAC rd53a_chuck_3 1
WAIT 1000
SETVAC rd53a_chuck_2 0

# Step 10. Check that HDI was moved to Chuck 3 and the adhesive was applied
@STEP_TEN CHOICEPOPUP $choice "Did you move the HDI to Chuck 3 and apply the adhesive?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_TEN $choice

# Step 11. Prompt placement of Vacuum Tool bearing HDI on Chuck 1
@STEP_ELEVEN CHOICEPOPUP $choice "Did you place the HDI on the MockROC on Chuck 1 (With adhesive between them)?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_ELEVEN $choice

# Step 12. Release vacuum of Vacuum Tool

SETVAC rd53a_chuck_3 0

# Step 13. Prompt replacement of Vacuum Tool on Chuck 3
@STEP_THIRTEEN CHOICEPOPUP $choice "Did you place the Vacuum Tool on Chuck 3?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_THIRTEEN $choice

# Step 14. Move gantry head to Chuck 1 and survey for Module accuracy

MOVESAFE $pos_chuck1_tr 100
@ACC_0 CHOICEPOPUP $choice "Ready to find the Top Right marker of HDI?" "Yes" "No"
GOTOIFN @ACC_0 $choice
CALL @SURVEYCORNERS $pos_chuck1_tr -> $pos_chuck1_tr

@ACC_1 CHOICEPOPUP $choice "Ready to find the Top Right marker of MockROC?" "Yes" "No"
GOTOIFN @ACC_1 $choice
CALL @SURVEYCORNERS $pos_senRoc1_tr -> $pos_senRoc1_tr


@ACC_2 CHOICEPOPUP $choice "Ready to find the Bottom Right marker of HDI?" "Yes" "No"
GOTOIFN @ACC_2 $choice
CALL @SURVEYCORNERS $pos_chuck1_br -> $pos_chuck1_br

@ACC_3 CHOICEPOPUP $choice "Ready to find the Bottom Right marker of MockROC?" "Yes" "No"
GOTOIFN @ACC_3 $choice
CALL @SURVEYCORNERS $pos_senRoc1_br -> $pos_senRoc1_br


@ACC_4 CHOICEPOPUP $choice "Ready to find the Bottom Left marker of HDI?" "Yes" "No"
GOTOIFN @ACC_4 $choice
CALL @SURVEYCORNERS $pos_chuck1_bl -> $pos_chuck1_bl

@ACC_5 CHOICEPOPUP $choice "Ready to find the Bottom Left marker of MockROC?" "Yes" "No"
GOTOIFN @ACC_5 $choice
CALL @SURVEYCORNERS $pos_senRoc1_bl -> $pos_senRoc1_bl


@ACC_6 CHOICEPOPUP $choice "Ready to find the Top Left marker of HDI?" "Yes" "No"
GOTOIFN @ACC_6 $choice
CALL @SURVEYCORNERS $pos_chuck1_tl -> $pos_chuck1_tl

@ACC_7 CHOICEPOPUP $choice "Ready to find the Top Left marker of MockROC?" "Yes" "No"
GOTOIFN @ACC_7 $choice
CALL @SURVEYCORNERS $pos_senRoc1_tl -> $pos_senRoc1_tl


FIT $pos_hdi_center_final $rot_hdi_final TFPX_RD53ASENSORROC $pos_chuck1_tr $pos_chuck1_br $pos_chuck1_bl $pos_chuck1_tl
FIT $pos_senRoc_final $rot_senRoc_final TFPX_RD53ASENSORROC $pos_senRoc1_tr $pos_senRoc1_br $pos_senRoc1_bl $pos_senRoc1_tl
QUAT2EULER $rot_hdi_final $trash0 $trash1 $rot_hdi_final
QUAT2EULER $rot_senRoc_final $trash0 $trash1 $rot_senRoc_final

PRINT "Final Centers and Rotations:"
PRINT "HDI Center: %v HDI Rotation: %f" $pos_hdi_center_final $rot_hdi_final
PRINT "MockROC Center: %v MockROC Rotation: %f" $pos_senRoc_final $rot_senRoc_final


SUB $pos_final_diff $pos_hdi_center_final $pos_senRoc_final
SUB $rot_final_diff $rot_hdi_final $rot_senRoc_final

PRINT "Differences between Centers and Rotations:"
PRINT "Center: %v Rotation: %f" $pos_final_diff $rot_final_diff
PRINT ""

COPY $pos_chuck1_tr.z 0
COPY $pos_chuck1_br.z 0
COPY $pos_chuck1_bl.z 0
COPY $pos_chuck1_tl.z 0
COPY $pos_senRoc1_tr.z 0
COPY $pos_senRoc1_br.z 0
COPY $pos_senRoc1_bl.z 0
COPY $pos_senRoc1_tl.z 0

SUB $dist_tr $pos_chuck1_tr $pos_senRoc1_tr
ABS $dist_tr $dist_tr
SUB $dist_br $pos_chuck1_br $pos_senRoc1_br
ABS $dist_br $dist_br
SUB $dist_bl $pos_chuck1_bl $pos_senRoc1_bl
ABS $dist_bl $dist_bl
SUB $dist_tl $pos_chuck1_tl $pos_senRoc1_tl
ABS $dist_tl $dist_tl

PRINT "Distance between HDI and MockROC"
PRINT "TR: %f BR: %f BL: %f TL: %f" $dist_tr $dist_br $dist_bl $dist_tl


ADD $dist_mean $dist_tr $dist_br
ADD $dist_mean $dist_mean $dist_bl
ADD $dist_mean $dist_mean $dist_tl
MUL $dist_mean $dist_mean 0.25

PRINT "Distance Mean: %f" $dist_mean


COPY $dist_dev 0

SUB $stddev_temp $dist_mean $dist_tr
MUL $stddev_temp $stddev_temp $stddev_temp
ADD $dist_dev $dist_dev $stddev_temp
SUB $stddev_temp $dist_mean $dist_br
MUL $stddev_temp $stddev_temp $stddev_temp
ADD $dist_dev $dist_dev $stddev_temp
SUB $stddev_temp $dist_mean $dist_bl
MUL $stddev_temp $stddev_temp $stddev_temp
ADD $dist_dev $dist_dev $stddev_temp
SUB $stddev_temp $dist_mean $dist_tl
MUL $stddev_temp $stddev_temp $stddev_temp
ADD $dist_dev $dist_dev $stddev_temp

MUL $dist_dev $dist_dev 0.25
POW $dist_dev $dist_dev 0.5

PRINT "Distance Standard Deviation: %f" $dist_dev
PRINT "--------------------"

# Step 15. End program with vacuums on or off
CHOICEPOPUP $choice "Do you want to leave the vacuums on?" "Yes, Leave them on" "No, Turn them off"
GOTOIF @STEP_FIFTEEN $choice

SETVAC gantry_head_inner 0
SETVAC gantry_head_outer 0
SETVAC rd53a_chuck_1_TL 0
SETVAC rd53a_chuck_1_TR 0
SETVAC rd53a_chuck_1_BL 0
SETVAC rd53a_chuck_1_BR 0
SETVAC rd53a_chuck_2 0
SETVAC rd53a_chuck_3 0
SETVAC launchpad 0

@STEP_FIFTEEN PASS

HOME

END

# SURVEYCORNERS
# Arguments
#  $corner - Approximate position of an object that you would like to know the precise position. Returns with precise position written in this location
#
# Returns
#  $corner - Updated position of the corner that is wanted
@SURVEYCORNERS(corner)
  MOVETO $corner 10
  @SURCORNPOP VIDEO
  CHOICEPOPUP $choice "Did you correctly align the cursor with the marker?" "Yes" "No"
  GOTOIFN @SURCORNPOP $choice
  GETPOS $corner
  RETURN $corner

# CALCPICKUP
# Arguments:
#  $target - center of piece to pickup/place
#
# Returns
#  $pick_pos - pickup position with the tool attached
#  $pick_pos_z0 - pickup position with the tool attached at z = 0
@CALCPICKUP(target)
  FLEXREAD $picker_height geometry.PICKER_TOOL.center_offset
  FLEXREAD $ghco geometry.tool_holder_offset
  ADD $netoffset $picker_height $ghco
  SUB $pick_pos $target $netoffset
  COPY $pick_pos_z0 $pick_pos
  COPY $pick_pos_z0.z 0
  RETURN $pick_pos $pick_pos_z0
